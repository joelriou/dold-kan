/-
Copyright (c) 2022 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/
import category_theory.preadditive.additive_functor
import category_theory.additive.basic
import category_theory.limits.shapes.biproducts
import category_theory.equivalence

/-!
# Pseudoabelian categories

-/

noncomputable theory

open category_theory
open category_theory.category
open category_theory.preadditive
open category_theory.limits
open_locale big_operators


namespace category_theory

namespace pseudoabelian

variables (C : Type*) [category C] [preadditive C]

@[nolint has_inhabited_instance]
structure karoubi := (X : C) (p : X âŸ¶ X) (idempotence : p â‰« p = p)

class is_pseudoabelian : Prop :=
(idempotents_have_kernels : Î  (P : karoubi C), has_kernel P.p)

namespace karoubi

variables {C}

@[ext]
lemma ext {P Q : karoubi C} (h_X : P.X = Q.X)
  (h_p : P.p â‰« eq_to_hom h_X = eq_to_hom h_X â‰« Q.p) : P = Q :=
begin
  cases P,
  cases Q,
  dsimp at h_X h_p,
  subst h_X,
  simpa only [true_and, eq_self_iff_true, id_comp, eq_to_hom_refl,
    heq_iff_eq, comp_id] using h_p,
end

@[simps]
def idempotent_of_id_sub_idempotent (P : karoubi C) : karoubi C :=
{ X := P.X,
  p := ğŸ™ _ - P.p,
  idempotence := by simp only [comp_sub, sub_comp, id_comp, comp_id, P.idempotence,
    sub_self, sub_zero], }

@[ext]
structure hom (P Q : karoubi C) := (f : P.X âŸ¶ Q.X) (comm : f = P.p â‰« f â‰« Q.p)

instance (P Q : karoubi C) : inhabited (hom P Q) := âŸ¨âŸ¨0, by rw [zero_comp, comp_zero]âŸ©âŸ©

@[ext]
lemma hom_ext {P Q : karoubi C} {f' g' : hom P Q} : f' = g' â†” f'.f = g'.f :=
by { split; intro h, { congr, assumption, }, { ext, assumption, }, }

lemma p_comp {P Q : karoubi C} (f : hom P Q) : P.p â‰« f.1 = f.1 :=
by rw [f.2, â† assoc, P.idempotence]

lemma comp_p {P Q : karoubi C} (f : hom P Q) : f.1 â‰« Q.p = f.1 :=
by rw [f.2, assoc, assoc, Q.idempotence]

lemma p_comm {P Q : karoubi C} (f : hom P Q) : P.p â‰« f.1 = f.1 â‰« Q.p :=
by rw [p_comp, comp_p]

def comp_proof {P Q R : karoubi C} (g' : hom Q R) (f' : hom P Q) :
  f'.1 â‰« g'.1 = P.p â‰« (f'.1 â‰« g'.1) â‰« R.p :=
by rw [assoc, comp_p, â† assoc, p_comp]

end karoubi

instance : category (karoubi C) :=
{ hom      := karoubi.hom,
  id       := Î» P, âŸ¨P.p, by { repeat { rw P.idempotence, }, }âŸ©,
  comp     := Î» P Q R f' g', âŸ¨f'.1 â‰« g'.1, karoubi.comp_proof g' f'âŸ©,
  id_comp' := Î» P Q f', by { ext, simp only [karoubi.p_comp], },
  comp_id' := Î» P Q f', by { ext, simp only [karoubi.comp_p], },
  assoc'   := Î» P Q R S f' g' h', by { ext, simp only [category.assoc], }, }

namespace karoubi

@[simp]
lemma comp {P Q R : karoubi C} (f' : P âŸ¶ Q) (g' : Q âŸ¶ R) :
  f' â‰« g' = âŸ¨f'.1 â‰« g'.1, comp_proof g' f'âŸ© := by refl

@[simp]
lemma id_eq {P : karoubi C} : ğŸ™ P = âŸ¨P.p, by repeat { rw P.idempotence, }âŸ© := by refl

instance coe : has_coe_t C (karoubi C) := âŸ¨Î» X, âŸ¨X, ğŸ™ X, by rw comp_idâŸ©âŸ©

@[simp]
lemma coe_X (X : C) : (X : karoubi C).X = X := by refl

@[simp]
lemma coe_p (X : C) : (X : karoubi C).p = ğŸ™ X := by refl

@[simp]
def eq_to_hom_f {P Q : karoubi C} (h : P = Q) :
  karoubi.hom.f (eq_to_hom h) = P.p â‰« eq_to_hom (congr_arg karoubi.X h) :=
by { subst h, simp only [eq_to_hom_refl, karoubi.id_eq, comp_id], }

end karoubi

@[simps]
def to_karoubi : C â¥¤ karoubi C := {
  obj := Î» X, âŸ¨X, ğŸ™ X, by rw comp_idâŸ©,
  map := Î» X Y f, âŸ¨f, by simp only [comp_id, id_comp]âŸ© }

instance : full (to_karoubi C) := {
  preimage := Î» X Y f', f'.1,
  witness' := Î» X Y f', by { ext, simp only [to_karoubi_map_f], }, }

instance : faithful (to_karoubi C) := { }

variables {C}

@[simps]
instance {P Q : karoubi C} : add_comm_group (P âŸ¶ Q) :=
{ add := Î» f' g', âŸ¨f'.1+g'.1, begin
    rw [add_comp, comp_add],
    congr',
    exact f'.2,
    exact g'.2,
  endâŸ©,
  zero := âŸ¨0, by simp only [comp_zero, zero_comp]âŸ©,
  zero_add := Î» f', by { ext, simp only [zero_add], },
  add_zero := Î» f', by { ext, simp only [add_zero], },
  add_assoc := Î» f' g' h', by simp only [add_assoc],
  add_comm := Î» f' g', by { ext, apply_rules [add_comm], },
  neg := Î» f', âŸ¨-f'.1, by simpa only [neg_comp, comp_neg, neg_inj] using f'.2âŸ©,
  add_left_neg := Î» f', by { ext, apply_rules [add_left_neg], }, }

namespace karoubi

lemma hom_eq_zero_iff {P Q : karoubi C} {f' : hom P Q} : f' = 0 â†” f'.f = 0 := by tidy

@[simps]
def inclusion_hom (P Q : karoubi C) : add_monoid_hom (P âŸ¶ Q) (P.X âŸ¶ Q.X) :=
{ to_fun   := Î» f', f'.1,
  map_zero' := rfl,
  map_add'  := Î» f' g', rfl }

def sum_hom {P Q : karoubi C} {Î± : Type*} (s : finset Î±) (f : Î± â†’ (P âŸ¶ Q)) :
  (âˆ‘ x in s, f x).1 = âˆ‘ x in s, (f x).1  := 
add_monoid_hom.map_sum (inclusion_hom P Q) f s

end karoubi

instance : preadditive (karoubi C) :=
{ hom_group := Î» P Q, by apply_instance,
  add_comp' := Î» P Q R f' g' h',
    by { ext, simp only [add_comp, quiver.hom.add_comm_group_add_f, karoubi.comp], },
  comp_add' := Î» P Q R f' g' h',
    by { ext, simp only [comp_add, quiver.hom.add_comm_group_add_f, karoubi.comp], }, }

instance : functor.additive (to_karoubi C) := { }

namespace karoubi

namespace biproducts

variables {C}
variables {J : Type*} [decidable_eq J] [fintype J] (F : J â†’ karoubi C)
variables [has_finite_biproducts C]

abbreviation biconeX := biproduct.bicone (Î» j, (F j).X)

abbreviation biconeX_p := biproduct.map (Î» j, (F j).p)

lemma biconeX_p_idempotence : biconeX_p F â‰« biconeX_p F = biconeX_p F :=
begin
  ext j,
  simp only [limits.biproduct.Î¹_map_assoc, limits.biproduct.Î¹_map],
  slice_lhs 1 2 { rw (F j).idempotence, },
end

@[simps]
def bicone : limits.bicone F :=
{ X :=
  { X := (biconeX F).X,
    p := (biconeX_p F),
      idempotence := biconeX_p_idempotence F, },
  Ï€ := Î» j, âŸ¨biconeX_p F â‰« (biconeX F).Ï€ j,
    by { simp only [limits.biproduct.map_Ï€_assoc, category.assoc,
      limits.biproduct.map_Ï€, (F j).idempotence], }âŸ©,
  Î¹ := Î» j, âŸ¨(biconeX F).Î¹ j â‰« biconeX_p F,
    by { simp only [limits.biproduct.Î¹_map, category.assoc],
      slice_rhs 1 3 { rw [(F j).idempotence, (F j).idempotence], }, }âŸ©,
  Î¹_Ï€ := Î» j j', begin
    split_ifs,
    { subst h,
      simp only [limits.biproduct.bicone_Î¹, limits.biproduct.Î¹_map,
        limits.biproduct.bicone_Ï€, limits.biproduct.Î¹_Ï€_self_assoc,
        comp, category.assoc, eq_to_hom_refl,
      limits.biproduct.map_Ï€, id_eq, (F j).idempotence], },
    { simp only [comp],
      conv { to_lhs, congr, rw assoc, congr, skip, rw â† assoc, congr,
        rw biconeX_p_idempotence, },
      simp only [limits.biproduct.bicone_Î¹, limits.biproduct.bicone_Ï€,
        limits.biproduct.map_Ï€],
      conv { to_lhs, congr, rw â† assoc, congr, rw (biconeX F).Î¹_Ï€, },
      split_ifs,
      simp only [hom_eq_zero_iff, zero_comp], },
  end, }

end biproducts

instance [has_finite_biproducts C] : has_finite_biproducts (karoubi C) :=
{ has_biproducts_of_shape := Î» J hJ1 hJ2,
  { has_biproduct := Î» F, begin
      letI := hJ2,
      apply has_biproduct_of_total (biproducts.bicone F),
      ext1, ext1,
      simp only [id_eq, comp_id, biproducts.bicone_X_p,
        limits.biproduct.Î¹_map],
      rw [sum_hom, comp_sum],
      rw finset.sum_eq_single j, rotate,
      { intros j' h1 h2,
        simp only [biproduct.Î¹_map, biproducts.bicone_Î¹_f, biproducts.bicone_Ï€_f, assoc, comp, biproduct.map_Ï€],
        slice_lhs 1 2 { rw biproduct.Î¹_Ï€, },
        split_ifs,
        { exfalso, exact h2 h.symm, },
        { simp only [zero_comp], } },
      { intro h1,
        exfalso,
        simpa only [finset.mem_univ, not_true] using h1, },
      simp only [biproducts.bicone_Ï€_f, comp,
        biproduct.Î¹_map, assoc, biproducts.bicone_Î¹_f, biproduct.map_Ï€],
      slice_lhs 1 2 { rw biproduct.Î¹_Ï€, },
      split_ifs, swap, { exfalso, exact h rfl, },
      simp only [eq_to_hom_refl, id_comp, (F j).idempotence],
    end, } }

end karoubi

open karoubi

theorem karoubi_is_pseudoabelian : is_pseudoabelian (karoubi C) :=
{ idempotents_have_kernels := Î» P, begin
    have h := P.idempotence,
    simp only [hom_ext, comp] at h,
    let Q : karoubi C := âŸ¨P.X.X, P.X.p - P.p.1,
      by { simp only [comp_sub, sub_comp, P.X.idempotence, p_comp, comp_p],
      simp only [comp_sub, sub_comp, P.X.idempotence,
        p_comp, comp_p, sub_zero, sub_self, h], }âŸ©,
    let Î¹ : Q âŸ¶ P.X := âŸ¨P.X.p - P.p.1,
      by simp only [sub_comp, comp_sub, id_comp, p_comp, comp_p,
        P.X.idempotence, h, sub_zero, sub_self],âŸ©,
    refine { exists_limit :=
      âŸ¨{ cone := limits.kernel_fork.of_Î¹ Î¹ _, is_limit := _ }âŸ© },
    { simp only [hom_eq_zero_iff, comp, sub_comp, p_comp, h, sub_self], },
    { refine is_limit.of_Î¹ _ _ _ _ _,
      { intros W g hg,
        refine âŸ¨g.1, _âŸ©,
        simp only [hom_eq_zero_iff, comp] at hg,
        simp only [Q, comp_sub, comp, hg, comp_zero, sub_zero],
        exact g.2, },
      { intros W g hg,
        simp only [hom_eq_zero_iff, comp] at hg,
        simp only [comp, comp_sub, hom_ext, hg, sub_zero, comp_p], },
      { intros W g hg g' hg',
        simpa only [hom_eq_zero_iff, hom_ext, comp, comp_sub, comp_p] using hg', }, }
  end }

instance [is_pseudoabelian C] : ess_surj (to_karoubi C) := âŸ¨Î» P,
begin
  let Q := idempotent_of_id_sub_idempotent P,
  let kernels := (show is_pseudoabelian C, by apply_instance).idempotents_have_kernels,
  haveI : has_kernel Q.p := kernels Q,
  have h := kernel.condition Q.p,
  simp only [idempotent_of_id_sub_idempotent_p, comp_sub, sub_eq_zero] at h,
  erw comp_id at h,  
  use kernel Q.p,
  apply nonempty.intro,
  refine
    { hom := âŸ¨kernel.Î¹ Q.p, _âŸ©,
      inv := âŸ¨kernel.lift Q.p P.p _, _âŸ©,
      inv_hom_id' := _,
      hom_inv_id' := _, },
  /- hom is well defined -/
  { erw [â† h, to_karoubi_obj_p, id_comp], },
  /- inv is well defined -/
  { simp only [comp_sub, idempotent_of_id_sub_idempotent_p, sub_eq_zero,
        P.idempotence], erw comp_id, },
  { slice_rhs 2 3 { erw [comp_id], },
    ext,
    simp only [assoc, kernel.lift_Î¹, P.idempotence], },
  /- inv_hom_id' -/
  { ext,
    simp only [equalizer_as_kernel, assoc, kernel.lift_Î¹,
      to_karoubi_obj_p, comp, assoc, id_eq],
    erw [â† h, id_comp], },
  /- hom_inv_id' -/
  { simp only [comp, id_eq, kernel.lift_Î¹], },
endâŸ©

variables (C)

def karoubi_is_equivalence [is_pseudoabelian C] : is_equivalence (to_karoubi C) :=
  equivalence.of_fully_faithfully_ess_surj (to_karoubi C)

namespace karoubi

variables {C}

@[simps]
def functor_extension {D : Type*} [category D] [preadditive D]
  (F : C â¥¤ D) : karoubi C â¥¤ karoubi D :=
{ obj := Î» P, âŸ¨F.obj P.X, F.map P.p, 
    by { rw â† F.map_comp, congr, exact P.idempotence, }âŸ©,
  map := Î» P Q f, âŸ¨F.map f.1,
    by { simp only [â† F.map_comp], congr, exact f.2, }âŸ©, }

@[simps]
def functor_extension' {D : Type*} [category D] [preadditive D]
  (F : C â¥¤ karoubi D) : karoubi C â¥¤ karoubi D :=
{ obj := Î» P, âŸ¨(F.obj P.X).X, (F.map P.p).1, begin
    have h := congr_arg (Î» (f : P.X âŸ¶ P.X), F.map f) P.idempotence,
    simpa only [F.map_comp, hom_ext] using h,
  endâŸ©,
  map := Î» P Q f, âŸ¨(F.map f.1).1, begin
    have h := congr_arg (Î» (f : P.X âŸ¶ Q.X), F.map f) f.2,
    simpa only [F.map_comp, hom_ext] using h,
  endâŸ©, }

@[simp]
def functor_extension'' {D : Type*} [category D] [preadditive D] [is_pseudoabelian D]
  (F : C â¥¤ D) : karoubi C â¥¤ D :=
  functor_extension F â‹™ (karoubi_is_equivalence D).inverse

@[simp]
lemma to_karoubi_comp_functor_extension' {D : Type*} [category D] [preadditive D]
  (F : C â¥¤ karoubi D) : to_karoubi C â‹™ karoubi.functor_extension' F = F :=
begin
  apply category_theory.functor.ext,
  { intros X Y f,
    ext,
    dsimp,
    simp only [karoubi.comp, karoubi.eq_to_hom_f, eq_to_hom_refl,
      to_karoubi_obj_p, karoubi.functor_extension'_obj_p, comp_id],
    erw [F.map_id, karoubi.id_eq, â† (F.map f).comm], },
  { intro X,
    ext,
    { dsimp,
      erw F.map_id,
      simp only [id_comp, karoubi.id_eq, comp_id], },
    { refl, }, },
end

@[simps]
def decomp_id_i (P : karoubi C) : P âŸ¶ P.X := âŸ¨P.p, by erw [coe_p, comp_id, P.idempotence]âŸ©

@[simps]
def decomp_id_p (P : karoubi C) : (P.X : karoubi C) âŸ¶ P := âŸ¨P.p, by erw [coe_p, id_comp, P.idempotence]âŸ©

lemma decomp_id (P : karoubi C) :
  ğŸ™ P = (decomp_id_i P) â‰« (decomp_id_p P) :=
by { ext, simp only [comp, id_eq, P.idempotence, decomp_id_i, decomp_id_p], }

def nat_trans_eq {D : Type*} [category D] {F G : karoubi C â¥¤ D} (Ï† : F âŸ¶ G) (P : karoubi C) :
  Ï†.app P = F.map (âŸ¨P.p, by erw [coe_p, comp_id, P.idempotence]âŸ© : P âŸ¶ P.X) â‰« Ï†.app P.X
    â‰« G.map (âŸ¨P.p, by erw [coe_p, id_comp, P.idempotence]âŸ©) :=
begin
  rw [â† Ï†.naturality, â† assoc, â† F.map_comp],
  conv { to_lhs, rw [â† id_comp (Ï†.app P), â† F.map_id], },
  congr,
  apply decomp_id,
end

lemma decomp_p (P : karoubi C) :
  (to_karoubi C).map P.p = (decomp_id_p P) â‰« (decomp_id_i P) :=
by { ext, simp only [comp, decomp_id_p_f, decomp_id_i_f, P.idempotence, to_karoubi_map_f], }

def decomp_id_i_to_karoubi (X : C) : decomp_id_i ((to_karoubi C).obj X) = ğŸ™ _ := by { ext, refl, }

def decomp_id_p_to_karoubi (X : C) : decomp_id_p ((to_karoubi C).obj X) = ğŸ™ _ := by { ext, refl, }

def decomp_id_i_naturality {P Q : karoubi C} (f : P âŸ¶ Q) : f â‰« decomp_id_i _ =
  decomp_id_i _ â‰« âŸ¨f.f, by erw [comp_id, id_comp]âŸ© :=
by { ext, simp only [comp, decomp_id_i_f, karoubi.comp_p, karoubi.p_comp], }

def decomp_id_p_naturality {P Q : karoubi C} (f : P âŸ¶ Q) : decomp_id_p P â‰« f =
  (âŸ¨f.f, by erw [comp_id, id_comp]âŸ© : (P.X : karoubi C) âŸ¶ Q.X) â‰« decomp_id_p Q :=
by { ext, simp only [comp, decomp_id_p_f, karoubi.comp_p, karoubi.p_comp], }

@[simps]
def functor_extension_hom_equiv {D : Type*} [category D] [preadditive D]
  (F G : C â¥¤ D) : (F âŸ¶ G) â‰ƒ (functor_extension F âŸ¶ functor_extension G) :=
{ to_fun := Î» Ï†,
  { app := Î» P,
    { f := F.map P.p â‰« Ï†.app P.X â‰« G.map P.p,
      comm := begin
        simp only [functor_extension_obj_p],
        slice_rhs 1 2 { rw [â† F.map_comp, P.idempotence], },
        slice_rhs 3 4 { rw [â† G.map_comp, P.idempotence], },
      end },
    naturality' := Î» P Q f, begin
      ext,
      simp only [functor_extension_map_f, comp, assoc, nat_trans.naturality_assoc],
      simp only [â† G.map_comp, karoubi.p_comp, â† assoc, karoubi.comp_p],
    end },
  inv_fun := Î» Ïˆ,
  { app := Î» X, (Ïˆ.app ((to_karoubi C).obj X)).f,
    naturality' := Î» X Y f, hom_ext.mp (Ïˆ.naturality ((to_karoubi C).map f)), },
  left_inv := Î» Ï†, begin
    ext X,
    dsimp,
    simp only [functor.map_id, id_comp, comp_id],
  end,
  right_inv := Î» Ïˆ, begin
    ext1,
    ext1 P,
    exact (nat_trans_eq Ïˆ P).symm,
  end } 

lemma functor_extension_hom_to_fun_comp {D : Type*} [category D] [preadditive D]
  {F G H : C â¥¤ D} (Ï† : F âŸ¶ G) (Ïˆ : G âŸ¶ H) :
  (functor_extension_hom_equiv F H).to_fun (Ï† â‰« Ïˆ) =
  (functor_extension_hom_equiv F G).to_fun Ï† â‰« (functor_extension_hom_equiv G H).to_fun Ïˆ :=
begin
  ext P,
  simp only [equiv.to_fun_as_coe, functor_extension_hom_equiv_apply_app_f, comp,
    assoc, nat_trans.naturality_assoc, nat_trans.comp_app, â† H.map_comp, P.idempotence],
end

lemma functor_extension_hom_to_fun_id {D : Type*} [category D] [preadditive D]
  {F : C â¥¤ D} :
  (functor_extension_hom_equiv F F).to_fun (ğŸ™ F) = ğŸ™ _ :=
begin
  ext P,
  simp only [equiv.to_fun_as_coe, functor_extension_hom_equiv_apply_app_f, id_eq,
    nat_trans.id_app, functor_extension_obj_p, id_comp, â† F.map_comp, P.idempotence],
end

lemma functor_extension_hom_inv_fun_comp {D : Type*} [category D] [preadditive D]
  {F G H : C â¥¤ D} (Ï† : functor_extension F âŸ¶ functor_extension G) (Ïˆ : functor_extension G âŸ¶ functor_extension H) :
  (functor_extension_hom_equiv F H).inv_fun (Ï† â‰« Ïˆ) =
  (functor_extension_hom_equiv F G).inv_fun Ï† â‰« (functor_extension_hom_equiv G H).inv_fun Ïˆ :=
begin
  ext X,
  simp only [comp, nat_trans.comp_app, equiv.inv_fun_as_coe,
    functor_extension_hom_equiv_symm_apply_app],
end

lemma functor_extension_hom_inv_fun_id {D : Type*} [category D] [preadditive D]
  {F : C â¥¤ D} :
  (functor_extension_hom_equiv F F).inv_fun (ğŸ™ (functor_extension F)) = ğŸ™ _ :=
begin
  ext X,
  simp only [to_karoubi_obj_p, id_eq, nat_trans.id_app, functor_extension_obj_p,
    equiv.inv_fun_as_coe, functor_extension_hom_equiv_symm_apply_app, F.map_id X],
end

@[simps]
def functor_extension_iso_equiv {D : Type*} [category D] [preadditive D]
  (F : C â¥¤ D) (G : C â¥¤ D) : (F â‰… G) â‰ƒ (functor_extension F â‰… functor_extension G) :=
{ to_fun := Î» Ï†,
  { hom := (functor_extension_hom_equiv F G).to_fun Ï†.hom,
    inv := (functor_extension_hom_equiv G F).to_fun Ï†.inv,
    hom_inv_id' := by rw [â† functor_extension_hom_to_fun_comp, Ï†.hom_inv_id, functor_extension_hom_to_fun_id],
    inv_hom_id' := by rw [â† functor_extension_hom_to_fun_comp, Ï†.inv_hom_id, functor_extension_hom_to_fun_id], },
  inv_fun := Î» Ïˆ,
  { hom := (functor_extension_hom_equiv F G).inv_fun Ïˆ.hom,
    inv := (functor_extension_hom_equiv G F).inv_fun Ïˆ.inv,
    hom_inv_id' := by rw [â† functor_extension_hom_inv_fun_comp, Ïˆ.hom_inv_id, functor_extension_hom_inv_fun_id],
    inv_hom_id' := by rw [â† functor_extension_hom_inv_fun_comp, Ïˆ.inv_hom_id, functor_extension_hom_inv_fun_id], },
  left_inv := Î» Ï†, by { ext1, exact (functor_extension_hom_equiv F G).left_inv Ï†.hom, },
  right_inv := Î» Ïˆ, by { ext1, exact (functor_extension_hom_equiv F G).right_inv Ïˆ.hom, }, }

@[simps]
def to_karoubi_hom_equiv {D : Type*} [category D]
  (F G : karoubi C â¥¤ D) : (F âŸ¶ G) â‰ƒ (to_karoubi _ â‹™ F âŸ¶ to_karoubi _ â‹™ G) :=
{ to_fun := Î» Ï†,
  { app := Î» X, Ï†.app ((to_karoubi C).obj X),
    naturality' := Î» X Y f, by simp only [nat_trans.naturality, functor.comp_map], },
  inv_fun := Î» Ïˆ,
  { app := Î» P, F.map (decomp_id_i P) â‰« (Ïˆ.app P.X) â‰« G.map (decomp_id_p P),
    naturality' := Î» P Q f, by {
      slice_lhs 1 2 { rw [â† F.map_comp], },
      slice_rhs 3 4 { rw [â† G.map_comp], },
      rw [decomp_id_i_naturality, decomp_id_p_naturality,
        F.map_comp, G.map_comp],
      slice_lhs 2 3 { erw Ïˆ.naturality, },
      simp only [assoc],
      refl, }, },
  left_inv := Î» Ï†, by { ext P, exact (nat_trans_eq Ï† P).symm, },
  right_inv := Î» Ïˆ, begin
    ext X,
    dsimp,
    erw [decomp_id_i_to_karoubi, decomp_id_p_to_karoubi,
      F.map_id, G.map_id, comp_id, id_comp],
  end }

lemma to_karoubi_hom_inv_fun_comp {D : Type*} [category D]
  {F G H : karoubi C â¥¤ D} (Ï† : to_karoubi _ â‹™ F âŸ¶ to_karoubi _ â‹™ G)
  (Ïˆ : to_karoubi _ â‹™ G âŸ¶ to_karoubi _ â‹™  H) :
  (to_karoubi_hom_equiv F H).inv_fun (Ï† â‰« Ïˆ) =
  (to_karoubi_hom_equiv F G).inv_fun Ï† â‰« (to_karoubi_hom_equiv G H).inv_fun Ïˆ :=
begin
  ext P,
  dsimp,
  slice_rhs 3 4 { rw [â† G.map_comp, â† decomp_p], },
  erw Ïˆ.naturality P.p,
  slice_rhs 4 5 { erw [â† H.map_comp], },
  simp only [assoc],
  congr,
  ext,
  simp only [decomp_id_p_f, comp, to_karoubi_map_f, P.idempotence],
end

lemma to_karoubi_hom_inv_fun_id {D : Type*} [category D]
  {F : karoubi C â¥¤ D} :
  (to_karoubi_hom_equiv F F).inv_fun (ğŸ™ _) = ğŸ™ _ :=
begin
  ext P,
  simp only [to_karoubi_hom_equiv_symm_apply_app, nat_trans.id_app, equiv.inv_fun_as_coe],
  erw [id_comp, â† F.map_comp, â† decomp_id, F.map_id],
end

@[simps]
def to_karoubi_iso_equiv {D : Type*} [category D]
  (F G : karoubi C â¥¤ D) : (F â‰… G) â‰ƒ (to_karoubi _ â‹™ F â‰… to_karoubi _ â‹™ G) :=
{ to_fun := Î» Ï†,
  { hom := (to_karoubi_hom_equiv F G).to_fun Ï†.hom,
    inv := (to_karoubi_hom_equiv G F).to_fun Ï†.inv, },
  inv_fun := Î» Ïˆ,
  { hom := (to_karoubi_hom_equiv F G).inv_fun Ïˆ.hom,
    inv := (to_karoubi_hom_equiv G F).inv_fun Ïˆ.inv,
    hom_inv_id' := by rw [â† to_karoubi_hom_inv_fun_comp, iso.hom_inv_id, to_karoubi_hom_inv_fun_id],
    inv_hom_id' := by rw [â† to_karoubi_hom_inv_fun_comp, iso.inv_hom_id, to_karoubi_hom_inv_fun_id], },
  left_inv := Î» Ï†, by { ext P, simp only [equiv.to_fun_as_coe, equiv.symm_apply_apply,
    equiv.inv_fun_as_coe], },
  right_inv := Î» Ïˆ, by { ext X, simp only [equiv.to_fun_as_coe, equiv.apply_symm_apply,
    equiv.inv_fun_as_coe], } }

end karoubi

namespace karoubi_karoubi

@[simps]
def inverse : karoubi (karoubi C) â¥¤ karoubi C :=
  { obj := Î» P, âŸ¨P.X.X, P.p.1,
      by simpa only [hom_ext] using P.idempotenceâŸ©,
    map := Î» P Q f, âŸ¨f.1.1,
      by simpa only [hom_ext] using f.2âŸ©, }

instance : functor.additive (inverse C) := { }

@[simps]
def unit_iso : ğŸ­ (karoubi C) â‰… to_karoubi (karoubi C) â‹™ inverse C :=
{ hom :=
  { app := Î» P, eq_to_hom (by { cases P, refl, }),
    naturality' := Î» P Q f,
      by { cases P, cases Q, cases f, dsimp [inverse],
        simp only [comp_id, id_comp, hom_ext], }, },
  inv :=
  { app := Î» P, eq_to_hom (by { cases P, refl, }),
    naturality' := Î» P Q f, begin
      cases P,
      cases Q,
      dsimp [inverse],
      simp only [comp_id, id_comp, hom_ext],
    end },
  hom_inv_id' := begin
    ext P,
    cases P,
    dsimp,
    simpa only [id_eq, hom_ext] using P_idempotence,
  end,
  inv_hom_id' := begin
    ext P,
    cases P,
    dsimp,
    simpa only [id_eq, hom_ext] using P_idempotence,
  end, }

@[simps]
def counit_iso : inverse C â‹™ to_karoubi (karoubi C) â‰… ğŸ­ (karoubi (karoubi C)) :=
{ hom := 
  { app := Î» P, âŸ¨âŸ¨P.p.1, begin
    have h := P.idempotence,
    simp only [hom_ext, comp] at h,
    erw [â† assoc, h, comp_p],
    endâŸ©,
    begin
      have h := P.idempotence,
      simp only [hom_ext, comp] at h âŠ¢,
      erw [h, h],
    endâŸ©,
    naturality' := Î» P Q f, begin
      have h := comp_p f,
      have h' := p_comp f,
      simp only [hom_ext] at h h' âŠ¢,
      erw [h, h'],
    end, },
  inv :=
  { app := Î» P, âŸ¨âŸ¨P.p.1, begin
      have h := P.idempotence,
      simp only [hom_ext, comp] at h,
      erw [h, p_comp],
    endâŸ©,
    begin
      have h := P.idempotence,
      simp only [hom_ext, comp] at h âŠ¢,
      erw [h, h],
    endâŸ©,
    naturality' := Î» P Q f, begin
      have h := comp_p f,
      have h' := p_comp f,
      simp only [hom_ext] at h h' âŠ¢,
      erw [h, h'],
    end, },
  hom_inv_id' := begin
    ext P,
    dsimp,
    simpa only [hom_ext, id_eq] using P.idempotence,
  end,
  inv_hom_id' := begin
    ext P,
    dsimp,
    simpa only [hom_ext, id_eq] using P.idempotence,
  end, }

end karoubi_karoubi

@[simps]
def karoubi_karoubi_equivalence : karoubi C â‰Œ karoubi (karoubi C) :=
{ functor := to_karoubi (karoubi C),
  inverse := karoubi_karoubi.inverse C,
  unit_iso := karoubi_karoubi.unit_iso C,
  counit_iso := karoubi_karoubi.counit_iso C,
  functor_unit_iso_comp' := Î» P, begin
    cases P,
    dsimp [karoubi_karoubi.unit_iso, karoubi_karoubi.counit_iso, to_karoubi],
    simp only [comp, id_eq, subtype.coe_mk, P_idempotence],
  end, }

instance : functor.additive (karoubi_karoubi_equivalence C).functor :=
  by { dsimp, apply_instance, }

instance : functor.additive (karoubi_karoubi_equivalence C).inverse :=
  by { dsimp, apply_instance, }

end pseudoabelian

end category_theory

