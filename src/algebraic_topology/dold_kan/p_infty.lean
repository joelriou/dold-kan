/-
Copyright (c) 2022 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Author: Jo√´l Riou
-/

import algebraic_topology.dold_kan.projections
import for_mathlib.idempotents.functor_categories
import for_mathlib.idempotents.functor_extension

/-

# Construction of the projection `P_infty` for the Dold-Kan correspondence

In this file, we construct the projection `P_infty : K[X] ‚ü∂ K[X]` by passing
to the limit the projections `P q` defined in `projections.lean`. This
projection is a critical tool in this formalisation of the Dold-Kan correspondence,
because in the case of abelian categories, `P_infty` corresponds to the
projection on the normalized Moore subcomplex, with kernel the degenerate subcomplex.
(See `equivalence.lean` for the general strategy of proof.)

-/

open category_theory
open category_theory.category
open category_theory.limits
open category_theory.preadditive
open category_theory.simplicial_object
open category_theory.idempotents
open simplex_category
open opposite
open_locale simplicial dold_kan

noncomputable theory

namespace algebraic_topology

namespace dold_kan

universe v

variables {C : Type*} [category.{v} C] [preadditive C]
variables {X : simplicial_object C}

lemma P_is_eventually_constant {q n : ‚Ñï} (hqn : n‚â§q) :
  ((P (q+1)).f n : X _[n] ‚ü∂ _ ) = (P q).f n :=
begin
  cases n,
  { simp only [P_deg0_eq], },
  { unfold P,
    simp only [add_right_eq_self, comp_add, homological_complex.comp_f,
      homological_complex.add_f_apply, comp_id],
    exact HœÉœÜ_eq_zero (nat.succ_le_iff.mp hqn) (higher_faces_vanish_P q n), }
end

/-- Definition of P_infty from the P q -/
def P_infty : K[X] ‚ü∂ K[X] := chain_complex.of_hom _ _ _ _ _ _
    (Œª n, ((P n).f n : X _[n] ‚ü∂ _ ))
begin
  intro n,
  simp only [‚Üê P_is_eventually_constant (rfl.ge : n‚â§n)],
  have eq := (P (n+1) : K[X] ‚ü∂ _).comm (n+1) n,
  erw chain_complex.of_d at eq,
  exact eq,
end

lemma P_infty_degreewise (n : ‚Ñï) :
  (P_infty.f n : X _[n] ‚ü∂  X _[n] ) = (P n).f n := by refl

lemma P_infty_degreewise_is_a_projection (n : ‚Ñï) :
  (P_infty.f n : X _[n] ‚ü∂ _) ‚â´ (P_infty.f n) = P_infty.f n :=
by simp only [P_infty_degreewise, P_degreewise_is_a_projection]

lemma P_infty_is_a_projection : (P_infty : K[X] ‚ü∂ _) ‚â´ P_infty = P_infty :=
by { ext n, exact P_infty_degreewise_is_a_projection n, }

lemma P_infty_degreewise_naturality (n : ‚Ñï) {X Y : simplicial_object C} (f : X ‚ü∂ Y) :
  f.app (op [n]) ‚â´ P_infty.f n = P_infty.f n ‚â´ f.app (op [n]) :=
P_degreewise_naturality n n f

variable (C)

/-- `P_infty` induces a natural transformation, i.e. an endomorphism of
the functor `alternating_face_map_complex C`. -/
@[simps]
def nat_trans_P_infty :
  alternating_face_map_complex C ‚ü∂ alternating_face_map_complex C :=
{ app := Œª _, P_infty,
  naturality' := Œª X Y f, by { ext n, exact P_infty_degreewise_naturality n f, }, }

/-- The natural transformation in each degree that is induced by `nat_trans_P_infty`. -/
@[simps]
def nat_trans_P_infty_degreewise (n : ‚Ñï) :=
nat_trans_P_infty C ‚ó´ ùüô (homological_complex.eval _ _ n)

variable {C}

@[simp]
lemma map_P_infty_degreewise {D : Type*} [category.{v} D] [preadditive D]
  (G : C ‚•§ D) [G.additive] (X : simplicial_object C) (n : ‚Ñï) :
  (P_infty : K[((whiskering C D).obj G).obj X] ‚ü∂ _).f n =
  G.map ((P_infty : alternating_face_map_complex.obj X ‚ü∂ _).f n) :=
by simp only [P_infty_degreewise, map_P]

/-- Given an object `Y : karoubi (simplicial_object C)`, this lemma
computes `P_infty` for the associated object in `simplicial_object (karoubi C)`
in terms of `P_infty` for `Y.X : simplicial_object C` and `Y.p`. -/
lemma karoubi_P_infty_f {Y : karoubi (simplicial_object C)} (n : ‚Ñï) :
  ((P_infty : K[(karoubi_functor_category_embedding _ _).obj Y] ‚ü∂ _).f n).f =
  Y.p.app (op [n]) ‚â´ (P_infty : K[Y.X] ‚ü∂ _).f n :=
begin
  -- We introduce P_infty endomorphisms P‚ÇÅ, P‚ÇÇ, P‚ÇÉ, P‚ÇÑ on various objects Y‚ÇÅ, Y‚ÇÇ, Y‚ÇÉ, Y‚ÇÑ.
  let Y‚ÇÅ := (karoubi_functor_category_embedding _ _).obj Y,
  let Y‚ÇÇ := Y.X,
  let Y‚ÇÉ := (((whiskering _ _).obj (to_karoubi C)).obj Y.X),
  let Y‚ÇÑ := (karoubi_functor_category_embedding _ _).obj ((to_karoubi _).obj Y.X),
  let P‚ÇÅ : K[Y‚ÇÅ] ‚ü∂ _ := P_infty,
  let P‚ÇÇ : K[Y‚ÇÇ] ‚ü∂ _ := P_infty,
  let P‚ÇÉ : K[Y‚ÇÉ] ‚ü∂ _ := P_infty,
  let P‚ÇÑ : K[Y‚ÇÑ] ‚ü∂ _ := P_infty,
  -- The statement of lemma relates P‚ÇÅ and P‚ÇÇ.
  suffices h‚ÇÅ‚ÇÇ : (P‚ÇÅ.f n).f = Y.p.app (op [n]) ‚â´ P‚ÇÇ.f n,
  { exact h‚ÇÅ‚ÇÇ, },
  -- The proof proceeds by obtaining relations h‚ÇÉ‚ÇÇ, h‚ÇÑ‚ÇÉ, h‚ÇÅ‚ÇÑ.
  have h‚ÇÉ‚ÇÇ : (P‚ÇÉ.f n).f = P‚ÇÇ.f n :=
    karoubi.hom_ext.mp (map_P_infty_degreewise (to_karoubi C) Y‚ÇÇ n),
  have h‚ÇÑ‚ÇÉ : P‚ÇÑ.f n = P‚ÇÉ.f n,
  { have eq := nat_trans_P_infty_degreewise_app (karoubi C) n,
    erw [‚Üê eq Y‚ÇÉ, ‚Üê eq Y‚ÇÑ],
    congr,
    exact congr_obj (to_karoubi_comp_karoubi_functor_category_embedding _ _) Y‚ÇÇ, },
  let œÑ‚ÇÅ := ùüô (karoubi_functor_category_embedding (simplex_category·µí·µñ) C),
  let œÑ‚ÇÇ := nat_trans_P_infty_degreewise (karoubi C) n,
  let œÑ := œÑ‚ÇÅ ‚ó´ œÑ‚ÇÇ,
  have h‚ÇÅ‚ÇÑ := idempotents.nat_trans_eq' œÑ Y,
  dsimp [œÑ, œÑ‚ÇÅ, œÑ‚ÇÇ, nat_trans_P_infty_degreewise] at h‚ÇÅ‚ÇÑ,
  erw [id_comp, id_comp, comp_id, comp_id] at h‚ÇÅ‚ÇÑ,
  erw [h‚ÇÅ‚ÇÑ, ‚Üê h‚ÇÉ‚ÇÇ, ‚Üê h‚ÇÑ‚ÇÉ],
  simp only [karoubi_functor_category_embedding.map_app_f, karoubi.decomp_id_p_f,
    karoubi.decomp_id_i_f, karoubi.comp],
  let œÄ : Y‚ÇÑ ‚ü∂ Y‚ÇÑ := (to_karoubi _ ‚ãô karoubi_functor_category_embedding _ _).map Y.p,
  have eq := karoubi.hom_ext.mp (P_infty_degreewise_naturality n œÄ),
  simp only [karoubi.comp] at eq,
  erw [‚Üê eq, ‚Üê assoc],
  congr,
  exact congr_app Y.idem (op [n]),
end

end dold_kan

end algebraic_topology
