/-
Copyright (c) 2022 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/

import algebraic_topology.dold_kan.faces

/-

# Construction of projections for the Dold-Kan correspondence

In this file, we construct endomorphisms `P q : K[X] âŸ¶ K[X]` for all
`q : â„•`. We study how they behave with respect to face maps with the lemmas
`higher_faces_vanish_P` and `P_is_identity_where_faces_vanish`.

Then, we show that they are projections (see `P_degreewise_is_a_projection`
and `P_is_a_projection`).  They are natural transformations (see `nat_trans_P`
and `P_degreewise_naturality`) and are compatible with the application
of additive functors (see `map_P`).

By passing to the limit, these endomorphisms `P q` shall be used in `p_infty.lean`
in order to define `P_infty : K[X] âŸ¶ K[X]`, see `equivalence.lean` for the general
strategy of proof of the Dold-Kan equivalence.

-/

open category_theory
open category_theory.category
open category_theory.limits
open category_theory.preadditive
open category_theory.simplicial_object
open opposite
open_locale simplicial dold_kan

noncomputable theory

namespace algebraic_topology

namespace dold_kan

variables {C : Type*} [category C] [preadditive C]
variables {X : simplicial_object C}

/-- This is the inductive definition of the projections `P q : K[X] âŸ¶ K[X]`,
with `P 0 := ğŸ™ _` and `P (q+1) := P q â‰« (ğŸ™ _ + HÏƒ q)`. -/
@[simp]
noncomputable def P : â„• â†’ (K[X] âŸ¶ K[X])
| 0     := ğŸ™ _
| (q+1) := P q â‰« (ğŸ™ _ + HÏƒ q)

/-- Q q is the complement projection associated to P q -/
def Q (q : â„•) : K[X] âŸ¶ K[X] := ğŸ™ _ - P q

lemma P_add_Q (q : â„•) : P q + Q q = ğŸ™ K[X] := by { rw Q, abel }

lemma P_add_Q_degreewise (q n : â„•) : (P q).f n + (Q q).f n = ğŸ™ (X _[n]) :=
by simpa only [â† homological_complex.add_f_apply, P_add_Q q]

lemma Q_eq_0 : (Q 0 : K[X] âŸ¶ _) = 0 := sub_self _

lemma Q_eq (q : â„•) : (Q (q+1) : K[X] âŸ¶ _) = Q q - P q â‰« HÏƒ q :=
by { unfold Q P, simp only [comp_add, comp_id], abel, }

/-- All the `Q q` coincide with `0` in degree 0. -/
lemma Q_deg0_eq (q : â„•) : ((Q q).f 0 : X _[0] âŸ¶ X _[0]) = 0 :=
begin
  induction q with q hq,
  { simpa only [Q_eq_0], },
  { rw Q_eq,
    simp only [Q_eq, hq, HÏƒ_eq_zero, homological_complex.sub_f_apply,
      homological_complex.comp_f, comp_zero, sub_zero], }
end

/-- All the `P q` coincide with `ğŸ™ _` in degree 0. -/
lemma P_deg0_eq (q : â„•) : ((P q).f 0 : X _[0] âŸ¶ X _[0]) = ğŸ™ _ :=
by conv_rhs { erw [â† P_add_Q_degreewise q 0, Q_deg0_eq, add_zero], }

/-- This lemma expresses the vanishing of
`(P q).f (n+1) â‰« X.Î´ k : X _[n+1] âŸ¶ X _[n]` when kâ‰ 0 and kâ‰¥n-q+2 -/
lemma higher_faces_vanish_P : Î  (q : â„•),
  Î  (n : â„•), higher_faces_vanish q (((P q).f (n+1) : X _[n+1] âŸ¶ X _[n+1]))
| 0     := Î» n j hjâ‚, by { exfalso, have hjâ‚‚ := fin.is_lt j, linarith, }
| (q+1) := Î» n, begin
    unfold P,
    exact higher_faces_vanish_induction (higher_faces_vanish_P q n),
  end

lemma P_is_identity_where_faces_vanish {Y : C} {n q : â„•} {Ï† : Y âŸ¶ X _[n+1]}
  (v : higher_faces_vanish q Ï†) : Ï† â‰« (P q).f (n+1) = Ï† :=
begin
  induction q with q hq,
  { unfold P,
    erw comp_id, },
  { unfold P,
    simp only [comp_add, homological_complex.comp_f,
      homological_complex.add_f_apply, comp_id, â† assoc,
      hq (downgrade_vanishing v), add_right_eq_self],
    by_cases hqn : n<q,
    { exact HÏƒÏ†_eq_zero hqn (downgrade_vanishing v), },
    { cases nat.le.dest (not_lt.mp hqn) with a ha,
      have hnaq : n=a+q := by linarith,
      simp only [HÏƒÏ†_eq_neg_ÏƒÎ´Ï† hnaq (downgrade_vanishing v), neg_eq_zero, â† assoc],
      have eq := v âŸ¨a, by linarithâŸ© _, swap,
      { have foo := nat.succ_eq_add_one,
        simp only [hnaq, fin.coe_mk, nat.succ_eq_add_one, add_assoc], },
      simp only [fin.succ_mk] at eq,
      simp only [eq, zero_comp], }, },
end

lemma P_degreewise_is_a_projection (q n : â„•) :
  ((P q).f n : X _[n] âŸ¶ _) â‰« ((P q).f n) = (P q).f n :=
begin
  cases n,
  { rw [P_deg0_eq q, comp_id], },
  { exact P_is_identity_where_faces_vanish (higher_faces_vanish_P q n), }
end

lemma P_is_a_projection (q : â„•) : (P q : K[X] âŸ¶ K[X]) â‰« P q = P q :=
by { ext n, exact P_degreewise_is_a_projection q n, }

/-- For each q, P q is a natural transformation. -/
def nat_trans_P (q : â„•) :
  alternating_face_map_complex C âŸ¶ alternating_face_map_complex C :=
{ app := Î» X, P q,
  naturality' := Î» X Y f, begin
    induction q with q hq,
    { erw [id_comp, comp_id], },
    { unfold P,
      simp only [add_comp, comp_add, assoc, comp_id, hq],
      congr' 1,
      rw [â† assoc, hq, assoc],
      congr' 1,
      exact (nat_trans_HÏƒ q).naturality' f, }
  end }

lemma P_degreewise_naturality (q n : â„•) {X Y : simplicial_object C} (f : X âŸ¶ Y) :
  f.app (op [n]) â‰« (P q).f n = (P q).f n â‰« f.app (op [n]) :=
homological_complex.congr_hom ((nat_trans_P q).naturality f) n

lemma map_P {D : Type*} [category D] [preadditive D]
  (G : C â¥¤ D) [G.additive] (X : simplicial_object C) (q n : â„•) :
  ((P q : K[((whiskering C D).obj G).obj X] âŸ¶ _).f n) = G.map ((P q : K[X] âŸ¶ _).f n) :=
begin
  induction q with q hq,
  { erw [G.map_id], refl, },
  { unfold P,
    simp only [comp_add, homological_complex.comp_f, homological_complex.add_f_apply,
      comp_id, functor.map_add, functor.map_comp, hq, map_HÏƒ], }
end

end dold_kan

end algebraic_topology
