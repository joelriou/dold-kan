/-
Copyright (c) 2022 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/

import algebraic_topology.dold_kan.homotopies
import algebra.big_operators.fin
import data.nat.parity


/-
# Study of face maps for the Dold-Kan correspondence

In this file, we obtain the technical lemmas that are used in the file
`projections.lean` in order to obtain basic properties of the endomorphisms
`P q : K[X] âŸ¶ K[X]` with respect to face maps (see `homotopies.lean` for the
role of these endomorphisms in the overall strategy of proof).

The main lemma in this file is `higher_faces_vanish_induction`. It is based
on two technical lemmas `HÏƒÏ†_eq_neg_ÏƒÎ´Ï†` and `HÏƒÏ†_eq_zero`.

-/
open nat
open category_theory
open category_theory.limits
open category_theory.category
open category_theory.preadditive
open category_theory.simplicial_object
open_locale simplicial
open_locale big_operators

namespace algebraic_topology

namespace dold_kan

variables {C : Type*} [category C] [preadditive C]
variables {X : simplicial_object C}

/-- We shall say that a morphism `Ï† : Y âŸ¶ X _[n+1]` satisfies `higher_faces_vanish q Ï†`
when the compositions `Ï† â‰« X.Î´ j` are `0` for $j â‰¥ \max (1, n+2-q)$. When `q â‰¤ n+1`,
it basically means that the composition `Ï† â‰« X.Î´ j` are `0` for the `q` highest
possible values of a nonzero `j`. Otherwise, when `q â‰¥ n+2`, all the compositions
`Ï† â‰« X.Î´ j` for nonzero `j` vanish. -/
def higher_faces_vanish {Y : C} {n : â„•} (q : â„•) (Ï† : Y âŸ¶ X _[n+1]) : Prop :=
âˆ€ (j : fin (n+1)), (n+1 â‰¤ (j : â„•) + q) â†’ Ï† â‰« X.Î´ j.succ = 0

lemma downgrade_vanishing {Y : C} {n : â„•} {q : â„•} {Ï† : Y âŸ¶ X _[n+1]}
  (v : higher_faces_vanish (q+1) Ï†) : higher_faces_vanish q Ï† :=
Î» j hj, v j (by { rw â† add_assoc, exact le_add_right hj, })

/- For data.fin.basic -/
@[simp]
lemma fin.cast_le_one {n m : â„•} (h : n.succ.succ â‰¤ m.succ.succ) :
  fin.cast_le h 1 = 1 :=
by simpa only [fin.eq_iff_veq]

/- For algebra.big_operators.fin -/
@[to_additive]
lemma fin.prod_eq_of_fin_cast {Î² : Type*} [comm_monoid Î²] {a b : â„•} (f : fin b â†’ Î²) (h : a = b) :
  âˆ (i : fin a), f (fin.cast h i) = âˆ (i : fin b), f i :=
by { subst h, congr, ext, congr, ext, simp only [fin.coe_cast], }

@[to_additive]
lemma fin.prod_split {Î² : Type*} [comm_monoid Î²] {a b : â„•} (f : fin (a+b) â†’ Î²) :
  âˆ (i : fin (a+b)), f i =
  (âˆ (i : fin a), f (fin.cast_le le_self_add i)) * âˆ (i : fin b), f (fin.nat_add a i) :=
begin
  revert f a,
  induction b with b hb,
  { intros a f,
    simp only [fin.prod_univ_zero, mul_one],
    congr,
    ext i,
    congr,
    ext,
    simp only [fin.coe_cast_le], },
  { intros a f,
    have eq : (a+1)+b=(a+b).succ := by simpa only [add_assoc, add_comm 1],
    rw [fin.prod_univ_succ, â† mul_assoc, â† fin.prod_eq_of_fin_cast f eq,
      hb (Î» (i : fin((a+1)+b)), f (fin.cast eq i))],
    congr,
    { rw fin.prod_univ_cast_succ,
      congr, },
    { ext,
      simp only,
      congr,
      ext,
      simp only [fin.coe_cast, fin.coe_nat_add, fin.coe_succ, add_assoc, add_comm 1], }, }
end

@[to_additive]
lemma fin.prod_trunc {Î² : Type*} [comm_monoid Î²] {n a b : â„•}
  (h : n=a+b) (f : fin(n) â†’ Î²)
  (hf : âˆ€ (j : fin (b)), f (fin.cast h.symm (fin.nat_add a j)) = 1) :
  âˆ (i : fin (n)), f i =
  âˆ (i : fin (a)), f (fin.cast_le (nat.le.intro (h.symm)) i) :=
begin
  rw [â† fin.prod_eq_of_fin_cast f h.symm, fin.prod_split, fintype.prod_eq_one _ hf, mul_one],
  congr,
end

lemma HÏƒÏ†_eq_neg_ÏƒÎ´Ï† {Y : C} {n a q : â„•} (hnaq : n=a+q) {Ï† : Y âŸ¶ X _[n+1]}
  (v : higher_faces_vanish q Ï†) : Ï† â‰« (HÏƒ q).f (n+1) =
  - Ï† â‰« X.Î´ âŸ¨a+1, nat.succ_lt_succ (nat.lt_succ_iff.mpr (nat.le.intro (eq.symm hnaq)))âŸ© â‰«
  X.Ïƒ âŸ¨a, nat.lt_succ_iff.mpr (nat.le.intro (eq.symm hnaq))âŸ© :=
begin
  have hnaq_shift : Î  d : â„•, n+d=(a+d)+q,
  { intro d, rw [add_assoc, add_comm d, â† add_assoc, hnaq], },
  rw [HÏƒ, homotopy.null_homotopic_map'_f (c_mk (n+2) (n+1) rfl) (c_mk (n+1) n rfl),
    hÏƒ'_eq hnaq (c_mk (n+1) n rfl), hÏƒ'_eq (hnaq_shift 1) (c_mk (n+2) (n+1) rfl)],
  repeat { erw chain_complex.of_d, },
  simp only [alternating_face_map_complex.obj_d, eq_to_hom_refl, comp_id,
    comp_sum, sum_comp, comp_add],
  simp only [comp_zsmul, zsmul_comp, â† assoc, â† mul_zsmul],
  /- cleaning up the first sum -/
  rw fin.sum_trunc (hnaq_shift 2), swap,
  { rintro âŸ¨k, hkâŸ©,
    let i : fin (n+1) := âŸ¨a+k+1, by linarithâŸ©,
    have eq := v i (by { simp only [i, fin.coe_mk], linarith, }),
    have hi : fin.cast (hnaq_shift 2).symm (fin.nat_add (a+2) âŸ¨k, hkâŸ©) = i.succ,
    { ext, simp only [fin.nat_add_mk, fin.cast_mk, fin.coe_mk, fin.succ_mk], linarith, },
    rw [hi, eq, zero_comp, zsmul_zero], },
  /- cleaning up the second sum -/
  rw fin.sum_trunc (hnaq_shift 3), swap,
  { rintros âŸ¨k, hkâŸ©,
    simp only [fin.nat_add_mk, fin.cast_mk, assoc],
    let i : fin (n+1) := âŸ¨a+1+k, by linarithâŸ©,
    have h : fin.cast_succ (âŸ¨a+1, by linarithâŸ© : fin (n+1)) < i.succ,
    { simp only [fin.lt_iff_coe_lt_coe, fin.cast_succ_mk, fin.coe_mk, fin.succ_mk],
      linarith, },
    have Î´Ïƒ_rel := Î´_comp_Ïƒ_of_gt X h,
    simp only [fin.cast_succ_mk] at Î´Ïƒ_rel,
    conv_lhs at Î´Ïƒ_rel
    { simp only [fin.succ_mk, fin.succ_mk, show a+1+k+1+1 = a+3+k, by linarith], },
    have eq := v i (by { simp only [i, fin.coe_mk], linarith, }),
    simp only [Î´Ïƒ_rel, â† assoc, zero_comp, zsmul_zero, eq], },
  /- leaving out three specific terms -/
  conv_lhs { congr, skip, rw [fin.sum_univ_cast_succ, fin.sum_univ_cast_succ], },
  rw fin.sum_univ_cast_succ,
  /- the purpose of the following `simplif` is to create three subgoals in order
    to finish the proof -/
  have simplif : âˆ€ (a b c d e f : Y âŸ¶ X _[n+1]), b=f â†’ d+e=0 â†’ c+a=0 â†’ a+b+(c+d+e) = f,
  { intros a b c d e f h1 h2 h3,
    rw [add_assoc c d e, h2, add_zero, add_comm a b, add_assoc,
      add_comm a c, h3, add_zero, h1], },
  apply simplif,
  { /- b=f -/
    have eq : (-1 : â„¤)^(a+1) * (-1 : â„¤)^a = -1,
    { rw â† pow_add,
      apply neg_one_pow_of_odd,
      use a,
      linarith, },
    simp only [eq, fin.last, fin.cast_le_mk, fin.coe_mk, neg_smul, one_zsmul], },
  { /- d+e = 0 -/
    let b : fin (n+2) := âŸ¨a+1, nat.succ_lt_succ $ nat.lt_succ_iff.mpr $
      nat.le.intro (eq.symm hnaq)âŸ©,
    have eqâ‚ : X.Ïƒ b â‰« X.Î´ (fin.cast_succ b) = ğŸ™ _ := Î´_comp_Ïƒ_self _,
    have eqâ‚‚ : X.Ïƒ b â‰« X.Î´ b.succ = ğŸ™ _ := Î´_comp_Ïƒ_succ _,
    simp only [b, fin.cast_succ_mk, fin.succ_mk] at eqâ‚ eqâ‚‚,
    simp only [eqâ‚, eqâ‚‚, fin.last, assoc, fin.cast_succ_mk, fin.cast_le_mk, fin.coe_mk,
      comp_id, add_eq_zero_iff_eq_neg, â† neg_zsmul],
    congr,
    ring_exp,
    rw mul_one, },
  { /- c+a = 0 -/
    rw â† finset.sum_add_distrib,
    apply finset.sum_eq_zero,
    rintros âŸ¨i, hiâŸ© hâ‚€,
    have hia : (âŸ¨i, by linarithâŸ© : fin(n+2)) â‰¤ fin.cast_succ (âŸ¨a, by linarithâŸ© : fin(n+1)) :=
      by simpa only [fin.le_iff_coe_le_coe, fin.coe_mk, fin.cast_succ_mk, â† lt_succ_iff] using hi,
    simp only [fin.coe_mk, fin.cast_le_mk, fin.cast_succ_mk, fin.succ_mk, assoc,
      â† Î´_comp_Ïƒ_of_le X hia, add_eq_zero_iff_eq_neg, â† neg_zsmul],
    congr,
    ring_exp, },
end

lemma HÏƒÏ†_eq_zero {Y : C} {n q : â„•} (hqn : n<q) {Ï† : Y âŸ¶ X _[n+1]}
  (v : higher_faces_vanish q Ï†) : Ï† â‰« (HÏƒ q).f (n+1) = 0 :=
begin
  simp only [HÏƒ, homotopy.null_homotopic_map'_f (c_mk (n+2) (n+1) rfl) (c_mk (n+1) n rfl)],
  erw [hÏƒ'_eq_zero hqn (c_mk (n+1) n rfl), comp_zero, zero_add],
  by_cases hqn' : n+1<q,
  { rw [hÏƒ'_eq_zero hqn' (c_mk (n+2) (n+1) rfl), zero_comp, comp_zero], },
  { simp only [hÏƒ'_eq (show n+1=0+q, by linarith) (c_mk (n+2) (n+1) rfl),
      pow_zero, fin.mk_zero, one_zsmul, eq_to_hom_refl, comp_id],
    erw chain_complex.of_d,
    simp only [alternating_face_map_complex.obj_d, comp_sum],
    rw fin.sum_trunc (show n+3=2+(n+1), by linarith),
    { simp only [fin.sum_univ_cast_succ, fin.sum_univ_zero, zero_add],
      simp only [fin.last, fin.mk_zero, fin.cast_succ_zero, fin.cast_le_zero, fin.coe_zero,
        pow_zero, one_zsmul, fin.mk_one, fin.cast_le_one, fin.coe_one, pow_one, neg_smul,
        comp_neg],
      erw [Î´_comp_Ïƒ_self, Î´_comp_Ïƒ_succ, add_right_neg], },
    { intro j,
      have Î´Ïƒ_rel := Î´_comp_Ïƒ_of_gt X (_ : fin.cast_succ (0 : fin (n+1)) < j.succ), swap,
      { simpa only [fin.cast_succ_zero] using fin.succ_pos j, },
      simp only [fin.cast_succ_zero, cast_succ] at Î´Ïƒ_rel,
      have h : fin.cast (by rw add_comm 2) (fin.nat_add 2 j) = j.succ.succ,
      { ext, simp only [add_comm 2, fin.coe_cast, fin.coe_nat_add, fin.coe_succ], },
      simp only [comp_zsmul, h, Î´Ïƒ_rel, â† assoc, v j (by linarith),
        zero_comp, zsmul_zero], }, },
end

lemma higher_faces_vanish_induction {Y : C} {n q : â„•} {Ï† : Y âŸ¶ X _[n+1]}
  (v : higher_faces_vanish q Ï†) : higher_faces_vanish (q+1) (Ï† â‰« (ğŸ™ _ + HÏƒ q).f (n+1)) :=
begin
    intros j hjâ‚,
    simp only [add_comp, comp_add, homological_complex.add_f_apply, homological_complex.id_f],
    erw comp_id,
    -- when n < q, the result follows immediately from the assumption
    by_cases hqn : n<q,
    { rw [HÏƒÏ†_eq_zero hqn v, zero_comp, add_zero, v j (by linarith)], },
    -- we now assume that nâ‰¥q, and write n=a+q
    cases nat.le.dest (not_lt.mp hqn) with a ha,
    rw [HÏƒÏ†_eq_neg_ÏƒÎ´Ï† (show n=a+q, by linarith) v,
      neg_comp, add_neg_eq_zero, assoc, assoc],
    cases n with m hm,
    -- the boundary case n=0
    { simp only [nat.eq_zero_of_add_eq_zero_left ha, fin.eq_zero j,
        fin.mk_zero, fin.mk_one],
      erw [Î´_comp_Ïƒ_succ],
      simp only [fin.succ_zero_eq_one, comp_id], },
    -- in the other case, we need to write n as m+1
    -- then, we first consider the particular case j = a
    by_cases hjâ‚‚ : a = (j : â„•),
    { simp only [hjâ‚‚, fin.eta, Î´_comp_Ïƒ_succ, comp_id],
      congr,
      ext,
      simp only [fin.coe_succ, fin.coe_mk], },
    -- now, we assume j â‰  a (i.e. a < j)
    have haj : a<j := (ne.le_iff_lt hjâ‚‚).mp (by linarith),
    have hjâ‚ƒ := j.is_lt,
    have ham : aâ‰¤m,
    { by_contradiction,
      rw [not_le, â† nat.succ_le_iff] at h,
      linarith, },
    have ineqâ‚ : (fin.cast_succ (âŸ¨a, nat.lt_succ_iff.mpr hamâŸ© : fin(m+1)) < j),
    { rw fin.lt_iff_coe_lt_coe, exact haj, },
    erw Î´_comp_Ïƒ_of_gt X ineqâ‚,
    by_cases ham' : a<m,
    { -- case where `a<m`
      have ineqâ‚‚ : (fin.cast_succ (âŸ¨a+1, nat.succ_lt_succ ham'âŸ© : fin(m+1)) â‰¤ j),
      { simpa only [fin.le_iff_coe_le_coe] using nat.succ_le_iff.mpr haj, },
      have Î´Î´_rel := Î´_comp_Î´ X ineqâ‚‚,
      simp only [fin.cast_succ_mk, fin.eta] at Î´Î´_rel,
      slice_rhs 2 3 { erw â† Î´Î´_rel, },
      simp only [â† assoc, v j (by linarith), zero_comp], },
    { -- in the last case, a=m, q=1 and j=a+1
      have ham'' : a=m := le_antisymm ham (not_lt.mp ham'),
      have hq : q=1,
      { simpa [â† ham'', show a.succ=a+1, by refl, add_comm a, add_right_inj] using ha, },
      have hjâ‚„ : (âŸ¨a+1, by linarithâŸ© : fin (m+3)) = fin.cast_succ j,
      { ext,
        simp only [fin.coe_mk, fin.coe_cast_succ],
        linarith, },
      slice_rhs 2 3 { rw [hjâ‚„, Î´_comp_Î´_self], },
      simp only [â† assoc, v j (by linarith), zero_comp], },
end

end dold_kan

end algebraic_topology
