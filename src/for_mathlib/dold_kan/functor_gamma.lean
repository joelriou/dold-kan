/-
Copyright (c) 2022 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/

import category_theory.additive.basic
import for_mathlib.idempotents.functor_extension
import algebra.homology.homological_complex
import algebraic_topology.simplicial_object
import for_mathlib.simplex_category.simplex_category2
import for_mathlib.dold_kan.notations

/-!

# Construction of the inverse functor of the Dold-Kan equivalence

In this file, we construct the functor `Î“â‚€ : chain_complex C â„• â¥¤ simplicial_object C`
which shall be the inverse functor of the Dold-Kan equivalence in the case of abelian categories,
and more generally pseudoabelian categories. We also extend this functor `Î“â‚€` as a functor
`Î“â‚‚ : karoubi (chain_complex C â„•) â¥¤ karoubi (simplicial_object C)` on the idempotent
completion, and this functor shall be an equivalence of categories when `C` is any additive
category (see `equivalence_additive.lean`).

When `K` is a chain_complex, `Î“â‚€.obj K` is a simplicial object which sends `op Î”` to
the direct sum of copies of `K.X m` for any tuple `(Î”', Î±)` where `Î± : Î” â†’ Î”'`
is an epimorphism in `simplex_category`. The index set of this direct sum is denoted
`Î“_index_set Î”`.

-/

noncomputable theory

open category_theory
open category_theory.category
open category_theory.limits
open category_theory.idempotents
open opposite
open_locale simplicial dold_kan

namespace algebraic_topology

namespace dold_kan

/-- The index set which appears in the definition of the functor
`Î“â‚€ : chain_complex C â„• â¥¤ simplicial_object C`. -/
def Î“_index_set (Î” : simplex_category) := Î£ (Î”' : simplex_category), { Î± : Î” âŸ¶ Î”' // epi Î± }

namespace Î“_index_set

variables {Î”' Î” : simplex_category} (A : Î“_index_set Î”)

/-- The epimorphism in `simplex_category` associated to `A : Î“_index_set Î”` -/
def e := A.2.1

instance : epi A.e := A.2.2

lemma ext' : A = âŸ¨A.1, âŸ¨A.e, A.2.2âŸ©âŸ© := by tidy

lemma ext (Aâ‚ Aâ‚‚ : Î“_index_set Î”) (hâ‚ : Aâ‚.1 = Aâ‚‚.1)
  (hâ‚‚ : Aâ‚.e â‰« eq_to_hom hâ‚ = Aâ‚‚.e) : Aâ‚ = Aâ‚‚ :=
begin
  rcases Aâ‚ with âŸ¨Î”â‚, âŸ¨Î±â‚, hÎ±â‚âŸ©âŸ©,
  rcases Aâ‚‚ with âŸ¨Î”â‚‚, âŸ¨Î±â‚‚, hÎ±â‚‚âŸ©âŸ©,
  simp only at hâ‚,
  subst hâ‚,
  simp only [eq_to_hom_refl, comp_id, Î“_index_set.e] at hâ‚‚,
  congr',
end

instance : fintype (Î“_index_set Î”) :=
  fintype.of_injective ((Î» A, âŸ¨âŸ¨A.1.len,
  nat.lt_succ_iff.mpr (simplex_category.len_le_of_epi (infer_instance : epi A.e))âŸ©, A.e.to_order_homâŸ©) :
Î“_index_set Î” â†’ (sigma (Î» (k : fin (Î”.len+1)), (fin (Î”.len+1) â†’ fin (k+1)))))
begin
  rintros âŸ¨Î”â‚, Î±â‚âŸ© âŸ¨Î”â‚‚, Î±â‚‚âŸ© h,
  simp only at h,
  have hâ‚ƒ : Î”â‚ = Î”â‚‚ := by { ext1, simpa only [subtype.mk_eq_mk] using h.1, },
  subst hâ‚ƒ,
  refine ext _ _ rfl _,
  ext1, ext1,
  exact eq_of_heq h.2,
end

variable (Î”)

/-- The distinguished element in `Î“_index_set Î”` which corresponds to the
identity of `Î”`. -/
def id : Î“_index_set Î” := âŸ¨Î”, âŸ¨ğŸ™ _, by apply_instance,âŸ©âŸ©

instance : inhabited (Î“_index_set Î”) := âŸ¨id Î”âŸ©

variables {Î”}

lemma eq_id_iff : A = id _ â†” A.1 = Î” :=
begin
  split,
  { intro h,
    rw h,
    refl, },
  { intro h,
    rcases A with âŸ¨Î”', âŸ¨f, hfâŸ©âŸ©,
    simp only at h,
    subst h,
    refine ext _ _ rfl _,
    { haveI := hf,
      simp only [eq_to_hom_refl, comp_id],
      exact simplex_category.eq_id_of_epi f, }, },
end

lemma eq_id_iff' : A = id _ â†” A.1.len = Î”.len :=
begin
  rw eq_id_iff,
  split,
  { intro h,
    rw h, },
  { intro h,
    ext,
    exact h, },
end

variable (Î¸ : Î”' âŸ¶ Î”)

/-- When `A : Î“_index_set Î”` and `Î¸ : Î”' â†’ Î”` is a morphism in `simplex_category`,
the simplicial morphism `(Î“â‚€.obj _).map Î¸` sends the term of the direct sum corresponding
to `A` to the term corresponding to `A.pull Î¸`. It is given by the epimorphism `e`, which
appears in the epi-mono factorisation `Î¸ â‰« A.e = e â‰« m`. -/
def pull : Î“_index_set Î”' := âŸ¨_, âŸ¨factor_thru_image (Î¸ â‰« A.e), infer_instanceâŸ©âŸ©

lemma fac_pull : (A.pull Î¸).e â‰« image.Î¹ (Î¸ â‰« A.snd.val) = Î¸ â‰« A.e :=
image.fac (Î¸ â‰« A.e)

end Î“_index_set

variables {C : Type*} [category C] [additive_category C]
variables (K K' : chain_complex C â„•) (f : K âŸ¶ K')
variables {Î”'' Î”' Î” : simplex_category}

/-- `is_dâ‚€ i` is a simple condition used to check whether a monomorphism in
`simplex_category` is the coface maps `Î´ 0`. -/
@[nolint unused_arguments]
def is_dâ‚€ (i : Î”' âŸ¶ Î”) [mono i] : Prop := (Î”.len = Î”'.len+1) âˆ§ (i.to_order_hom 0 â‰  0)

namespace is_dâ‚€

lemma iff {j : â„•} {i : fin (j+2)} : is_dâ‚€ (simplex_category.Î´ i) â†” i = 0 :=
begin
  split,
  { rintro âŸ¨hâ‚, hâ‚‚âŸ©,
    by_contradiction,
    exact hâ‚‚ (fin.succ_above_ne_zero_zero h), },
  { intro h,
    subst h,
    split,
    { refl, },
    { apply fin.succ_ne_zero, }, }
end

lemma eq_dâ‚€ {n : â„•} {i : [n] âŸ¶ [n+1]} [mono i] (hi : is_dâ‚€ i) :
  i = simplex_category.Î´ 0 :=
begin
  cases simplex_category.eq_Î´_of_mono i with j h,
  unfreezingI { subst h, },
  rw iff at hi,
  rw hi,
end

end is_dâ‚€

namespace Î“â‚€

variables (Î”' Î”)

namespace obj

/-- In the definition of `(Î“â‚€.obj K).obj Î”` as a direct sum indexed by `A : Î“_index_set Î”`,
the summand `summand K Î” A` is `K.X A.1.len`. -/
def summand (A : Î“_index_set Î”) : C := K.X A.1.len

/-- The functor `Î“â‚€` sends a chain complex `K` to the simplicial object which
sends `Î”` to the direct sum of the objects `summand K Î” A` for all `A : Î“_index_set Î”` -/
def objâ‚‚ : C := âˆ (Î» (A : Î“_index_set Î”), summand K Î” A)

namespace termwise

/-- A monomorphism `i : Î”' âŸ¶ Î”` induces a morphism `K.X Î”.len âŸ¶ K.X Î”'.len` which
is the identity if `Î” = Î”'`, the differential on the complex `K` if `i = Î´ 0`, and
zero otherwise. -/
def map_mono (K : chain_complex C â„•) {Î”' Î” : simplex_category} (i : Î”' âŸ¶ Î”) [mono i] :
  K.X Î”.len âŸ¶ K.X Î”'.len :=
begin
  by_cases Î” = Î”',
  { apply eq_to_hom,
    congr', },
  { by_cases is_dâ‚€ i,
    { exact K.d Î”.len Î”'.len, },
    { exact 0, }, },
end

variables {Î”'' Î”' Î”}
variables (i' : Î”'' âŸ¶ Î”') [mono i'] (i : Î”' âŸ¶ Î”) [mono i]

variable (Î”)
lemma map_mono_id : map_mono K (ğŸ™ Î”) = ğŸ™ _ := by { unfold map_mono, tidy, }

variable {Î”}

lemma map_mono_dâ‚€ (hi : is_dâ‚€ i) : map_mono K i = K.d Î”.len Î”'.len :=
begin
  unfold map_mono,
  split_ifs,
  { exfalso,
    cases hi with h1 h2,
    rw h at h1,
    linarith, },
  refl,
end

lemma map_mono_eq_zero (hâ‚ : Â¬Î” = Î”') (hâ‚‚ : Â¬is_dâ‚€ i) : map_mono K i = 0 :=
by { unfold map_mono, split_ifs, refl, }

variables {K K'}

@[simp, reassoc]
lemma map_mono_naturality : map_mono K i â‰« f.f Î”'.len = f.f Î”.len â‰« map_mono K' i :=
begin
  unfold map_mono,
  split_ifs,
  { unfreezingI { subst h, },
    simp only [id_comp, eq_to_hom_refl, comp_id], },
  { rw homological_complex.hom.comm, },
  { rw [zero_comp, comp_zero], }
end

lemma simplex_category_non_epi_mono {Î”' Î” : simplex_category} (i : Î”' âŸ¶ Î”) [mono i] (hi : Â¬Î”=Î”'):
  âˆƒ (k : â„•), Î”.len = Î”'.len + (k + 1) :=
begin
  cases le_iff_exists_add.mp (simplex_category.len_le_of_mono (infer_instance : mono i)) with k h,
  cases k,
  { exfalso,
    exact hi (simplex_category.ext Î” Î”' h), },
  { exact âŸ¨k, hâŸ©, },
end

variable (K)

@[simp, reassoc]
lemma map_mono_comp : map_mono K i â‰« map_mono K i' = map_mono K (i' â‰« i) :=
begin
  /- case where i : Î”' âŸ¶ Î” is the identity -/
  by_cases h1 : Î” = Î”',
  { unfreezingI { subst h1, },
    simp only [simplex_category.eq_id_of_mono i,
      comp_id, id_comp, map_mono_id K, eq_to_hom_refl], },
  /- case where i' : Î”'' âŸ¶ Î”' is the identity -/
  by_cases h2 : Î”' = Î”'',
  { unfreezingI { subst h2, },
    simp only [simplex_category.eq_id_of_mono i',
      comp_id, id_comp, map_mono_id K, eq_to_hom_refl], },
  /- then the RHS is always zero -/
  cases simplex_category_non_epi_mono i h1 with k hk,
  cases simplex_category_non_epi_mono i' h2 with k' hk',
  have eq : Î”.len = Î”''.len + (k+k'+2) := by { rw hk' at hk, linarith, },
  rw map_mono_eq_zero K (i' â‰« i) _ _, rotate,
  { by_contradiction,
    simpa only [self_eq_add_right,h ] using eq, },
  { by_contradiction,
    dsimp [is_dâ‚€] at h,
    simp only [h.left, add_right_inj] at eq,
    linarith, },
  /- in all cases, the LHS is also zero,
  either by definition, or because d â‰« d = 0 -/
  by_cases h3 : is_dâ‚€ i,
  { by_cases h4 : is_dâ‚€ i',
    { rw [map_mono_dâ‚€ K i h3, map_mono_dâ‚€ K i' h4,
        homological_complex.d_comp_d], },
    { simp only [map_mono_eq_zero K i' h2 h4, comp_zero], }, },
  { simp only [map_mono_eq_zero K i h1 h3, zero_comp], },
end

end termwise

/-- The simplicial morphism on the simplicial object `Î“â‚€.obj K` induced by
a morphism `Î”' â†’ Î”` in `simplex_category` is defined on each summand
associated to an `A : Î“_index_set Î”` in terms of the epi-mono factorisation
of `Î¸ â‰« A.e`. -/
def map (K : chain_complex C â„•) {Î”' Î” : simplex_category} (Î¸ : Î”' âŸ¶ Î”) :
  objâ‚‚ K Î” âŸ¶ objâ‚‚ K Î”' :=
sigma.desc (Î» A, termwise.map_mono K (image.Î¹ (Î¸ â‰« A.e)) â‰«
  (sigma.Î¹ (summand K Î”') (A.pull Î¸)))

variables {Î” Î”'}

@[reassoc]
lemma map_on_summand (A : Î“_index_set Î”) {Î¸ : Î”' âŸ¶ Î”}
  {e : Î”' âŸ¶ Î”''} {i : Î”'' âŸ¶ A.1} [epi e] [mono i]
  (fac : e â‰« i = Î¸ â‰« A.e) : (sigma.Î¹ (summand K Î”) A) â‰« map K Î¸ =
  termwise.map_mono K i â‰« sigma.Î¹ (summand K Î”') âŸ¨Î”'', âŸ¨e, by apply_instanceâŸ©âŸ© :=
begin
  simp only [map, colimit.Î¹_desc, cofan.mk_Î¹_app, Î“_index_set.pull],
  have h := simplex_category.image_eq fac,
  unfreezingI { subst h, },
  congr,
  { exact simplex_category.image_Î¹_eq fac, },
  { dsimp only [Î“_index_set.pull],
    congr,
    exact simplex_category.factor_thru_image_eq fac, },
end

@[reassoc]
lemma map_on_summand' (A : Î“_index_set Î”) (Î¸ : Î”' âŸ¶ Î”) :
  (sigma.Î¹ (summand K Î”) A) â‰« map K Î¸ =
  termwise.map_mono K (image.Î¹ (Î¸ â‰« A.e)) â‰« sigma.Î¹ (summand K _) (A.pull Î¸) :=
map_on_summand K A (A.fac_pull Î¸)

end obj

/-- The functor `Î“â‚€ : chain_complex C â„• â¥¤ simplicial_object C`, on objects. -/
@[simps]
def obj (K : chain_complex C â„•) : simplicial_object C :=
{ obj := Î» Î”, obj.objâ‚‚ K (unop Î”),
  map := Î» Î” Î”' Î¸, obj.map K Î¸.unop,
  map_id' := Î» Î”, begin
    ext A,
    cases A,
    have fac : A.e â‰« ğŸ™ A.1 = ğŸ™ Î”.unop â‰« A.e := by rw [comp_id, id_comp],
    erw [obj.map_on_summand K A fac, obj.termwise.map_mono_id, id_comp, comp_id],
    unfreezingI { rcases A with âŸ¨Î”', âŸ¨e, heâŸ©âŸ©, },
    congr,
  end,
  map_comp' := Î» Î”'' Î”' Î” Î¸' Î¸, begin
    ext A,
    cases A,
    have fac : Î¸.unop â‰« Î¸'.unop â‰« A.e = (Î¸' â‰« Î¸).unop â‰« A.e := by rw [unop_comp, assoc],
    rw [â† image.fac (Î¸'.unop â‰« A.e), â† assoc,
      â† image.fac (Î¸.unop â‰« factor_thru_image (Î¸'.unop â‰« A.e)), assoc] at fac,
    simpa only [obj.map_on_summand'_assoc K A Î¸'.unop, obj.map_on_summand' K _ Î¸.unop,
      obj.termwise.map_mono_comp_assoc, obj.map_on_summand K A fac],
  end }

/-- The functor `Î“â‚€ : chain_complex C â„• â¥¤ simplicial_object C`, on objects. -/
@[simps]
def map {K K' : chain_complex C â„•} (f : K âŸ¶ K') : obj K âŸ¶ obj K' :=
{ app := Î» Î”, limits.sigma.map (Î» (A : Î“_index_set Î”.unop), f.f A.1.len),
  naturality' := Î» Î”' Î” Î¸, begin
    ext A,
    simpa only [obj_map, obj.map, Î¹_colim_map_assoc,
      discrete.nat_trans_app, cofan.mk_Î¹_app, image.as_Î¹, colimit.Î¹_desc_assoc,
      Î¹_colim_map, colimit.Î¹_desc, assoc] using obj.termwise.map_mono_naturality_assoc _ _ _,
  end, }

end Î“â‚€

/-- The functor `Î“â‚€ : chain_complex C â„• â¥¤ simplicial_object C`, which is
the inverse functor of the Dold-Kan equivalence in the category abelian
categories, or more generally pseudoabelian categories. -/
@[simps]
def Î“â‚€ : chain_complex C â„• â¥¤ simplicial_object C :=
{ obj := Î“â‚€.obj,
  map := Î» _ _, Î“â‚€.map,
  map_id' := Î» K, begin
    ext Î” A,
    simp only [Î“â‚€.map_app, discrete.nat_trans_app, Î¹_colim_map, nat_trans.id_app,
      homological_complex.id_f],
    erw [id_comp, comp_id],
  end,
  map_comp' := Î» K K' K'' f f', begin
    ext Î” A,
    simp only [Î“â‚€.map_app, homological_complex.comp_f, discrete.nat_trans_app,
      Î¹_colim_map, Î¹_colim_map_assoc, assoc, nat_trans.comp_app],
  end, }

/-- The inclusion of a summand of `K[Î“â‚€.obj K].X n` -/
abbreviation Î¹_Î“â‚€_summand {n : â„•}
  (A : Î“_index_set [n]) : Î“â‚€.obj.summand K [n] A âŸ¶ K[Î“â‚€.obj K].X n  :=
sigma.Î¹ (Î“â‚€.obj.summand K [n]) A

lemma eq_Î¹_Î“â‚€_summand {n : â„•} (A : Î“_index_set [n]) :
  sigma.Î¹ (Î“â‚€.obj.summand K A.1) (Î“_index_set.id A.1) â‰«
    (Î“â‚€.obj K).map A.e.op = Î¹_Î“â‚€_summand K A :=
begin
  rw [Î“â‚€.obj_map, quiver.hom.unop_op,
    Î“â‚€.obj.map_on_summand K (Î“_index_set.id A.1)
    (show A.e â‰« ğŸ™ _ = A.e â‰« ğŸ™ _, by refl),
    Î“â‚€.obj.termwise.map_mono_id, A.ext'],
  apply id_comp,
end

/-- The extension of `Î“â‚€ : chain_complex C â„• â¥¤ simplicial_object C`
on the idempotent completions. It shall be an equivalence of categories
for any additive category `C`. -/
@[simps]
def Î“â‚‚ : karoubi (chain_complex C â„•) â¥¤ karoubi (simplicial_object C) :=
(category_theory.idempotents.functor_extension'' _ _).obj Î“â‚€

end dold_kan

end algebraic_topology
