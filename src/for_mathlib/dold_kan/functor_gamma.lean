/-
Copyright (c) 2022 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/

import category_theory.additive.basic
import for_mathlib.idempotents.functor_extension2
import algebra.homology.homological_complex
import algebraic_topology.simplicial_object
import for_mathlib.simplex_category.factorisations
import category_theory.limits.shapes.images
import for_mathlib.dold_kan.notations
import for_mathlib.split_simplicial_object

/-!

# Construction of the inverse functor of the Dold-Kan equivalence

In this file, we construct the functor `Î“â‚€ : chain_complex C â„• â¥¤ simplicial_object C`
which shall be the inverse functor of the Dold-Kan equivalence in the case of abelian categories,
and more generally pseudoabelian categories. We also extend this functor `Î“â‚€` as a functor
`Î“â‚‚ : karoubi (chain_complex C â„•) â¥¤ karoubi (simplicial_object C)` on the idempotent
completion, and this functor shall be an equivalence of categories when `C` is any additive
category (see `equivalence_additive.lean`).

When `K` is a chain_complex, `Î“â‚€.obj K` is a simplicial object which sends `op Î”` to
the direct sum of copies of `K.X m` for any tuple `(Î”', Î±)` where `Î± : Î” â†’ Î”'`
is an epimorphism in `simplex_category`. The index set of this direct sum is denoted
`Î“_index_set Î”`.

-/

noncomputable theory

open category_theory
open category_theory.category
open category_theory.limits
open category_theory.idempotents
open opposite
open simplex_category
open_locale simplicial dold_kan

namespace simplex_category

namespace splitting_index_set

variables {Î”' Î” : simplex_category} (A : splitting_index_set Î”) (Î¸ : Î”' âŸ¶ Î”)

instance : strong_epi (factor_thru_image Î¸) :=
strong_epi_factor_thru_image_of_strong_epi_mono_factorisation
  (has_strong_epi_mono_factorisations.has_fac Î¸).some

instance : epi (factor_thru_image Î¸) := strong_epi.epi

/-- When `A : Î“_index_set Î”` and `Î¸ : Î”' â†’ Î”` is a morphism in `simplex_category`,
the simplicial morphism `(Î“â‚€.obj _).map Î¸` sends the term of the direct sum corresponding
to `A` to the term corresponding to `A.pull Î¸`. It is given by the epimorphism `e`, which
appears in the epi-mono factorisation `Î¸ â‰« A.e = e â‰« m`. -/
def pull : splitting_index_set Î”' := âŸ¨_, âŸ¨factor_thru_image (Î¸ â‰« A.e), infer_instanceâŸ©âŸ©

lemma fac_pull : (A.pull Î¸).e â‰« image.Î¹ (Î¸ â‰« A.snd.val) = Î¸ â‰« A.e :=
image.fac (Î¸ â‰« A.e)

end splitting_index_set

end simplex_category

namespace algebraic_topology

namespace dold_kan

variables {C : Type*} [category C] [additive_category C]
variables (K K' : chain_complex C â„•) (f : K âŸ¶ K')
variables {Î”'' Î”' Î” : simplex_category}

/-- `is_dâ‚€ i` is a simple condition used to check whether a monomorphism in
`simplex_category` is the coface maps `Î´ 0`. -/
@[nolint unused_arguments]
def is_dâ‚€ (i : Î”' âŸ¶ Î”) [mono i] : Prop := (Î”.len = Î”'.len+1) âˆ§ (i.to_order_hom 0 â‰  0)

namespace is_dâ‚€

lemma iff {j : â„•} {i : fin (j+2)} : is_dâ‚€ (simplex_category.Î´ i) â†” i = 0 :=
begin
  split,
  { rintro âŸ¨hâ‚, hâ‚‚âŸ©,
    by_contradiction,
    exact hâ‚‚ (fin.succ_above_ne_zero_zero h), },
  { intro h,
    subst h,
    split,
    { refl, },
    { apply fin.succ_ne_zero, }, }
end

lemma eq_dâ‚€ {n : â„•} {i : [n] âŸ¶ [n+1]} [mono i] (hi : is_dâ‚€ i) :
  i = simplex_category.Î´ 0 :=
begin
  cases simplex_category.eq_Î´_of_mono i with j h,
  unfreezingI { subst h, },
  rw iff at hi,
  rw hi,
end

end is_dâ‚€

namespace Î“â‚€

variables (Î”' Î”)

namespace obj

/-- In the definition of `(Î“â‚€.obj K).obj Î”` as a direct sum indexed by `A : Î“_index_set Î”`,
the summand `summand K Î” A` is `K.X A.1.len`. -/
def summand (A : splitting_index_set Î”) : C := K.X A.1.len

/-- The functor `Î“â‚€` sends a chain complex `K` to the simplicial object which
sends `Î”` to the direct sum of the objects `summand K Î” A` for all `A : Î“_index_set Î”` -/
def objâ‚‚ : C := âˆ (Î» (A : splitting_index_set Î”), summand K Î” A)

namespace termwise

/-- A monomorphism `i : Î”' âŸ¶ Î”` induces a morphism `K.X Î”.len âŸ¶ K.X Î”'.len` which
is the identity if `Î” = Î”'`, the differential on the complex `K` if `i = Î´ 0`, and
zero otherwise. -/
def map_mono (K : chain_complex C â„•) {Î”' Î” : simplex_category} (i : Î”' âŸ¶ Î”) [mono i] :
  K.X Î”.len âŸ¶ K.X Î”'.len :=
begin
  by_cases Î” = Î”',
  { apply eq_to_hom,
    congr', },
  { by_cases is_dâ‚€ i,
    { exact K.d Î”.len Î”'.len, },
    { exact 0, }, },
end

variables {Î”'' Î”' Î”}
variables (i' : Î”'' âŸ¶ Î”') [mono i'] (i : Î”' âŸ¶ Î”) [mono i]

variable (Î”)
lemma map_mono_id : map_mono K (ğŸ™ Î”) = ğŸ™ _ := by { unfold map_mono, tidy, }

variable {Î”}

lemma map_mono_dâ‚€ (hi : is_dâ‚€ i) : map_mono K i = K.d Î”.len Î”'.len :=
begin
  unfold map_mono,
  split_ifs,
  { exfalso,
    cases hi with h1 h2,
    rw h at h1,
    linarith, },
  refl,
end

lemma map_mono_dâ‚€' {n : â„•} : map_mono K (Î´ (0 : fin (n+2))) = K.d (n+1) n :=
map_mono_dâ‚€ K _ (by rw is_dâ‚€.iff)

lemma map_mono_eq_zero (hâ‚ : Â¬Î” = Î”') (hâ‚‚ : Â¬is_dâ‚€ i) : map_mono K i = 0 :=
by { unfold map_mono, split_ifs, refl, }

variables {K K'}

@[simp, reassoc]
lemma map_mono_naturality : map_mono K i â‰« f.f Î”'.len = f.f Î”.len â‰« map_mono K' i :=
begin
  unfold map_mono,
  split_ifs,
  { unfreezingI { subst h, },
    simp only [id_comp, eq_to_hom_refl, comp_id], },
  { rw homological_complex.hom.comm, },
  { rw [zero_comp, comp_zero], }
end

lemma simplex_category_non_epi_mono {Î”' Î” : simplex_category} (i : Î”' âŸ¶ Î”) [mono i] (hi : Â¬Î”=Î”'):
  âˆƒ (k : â„•), Î”.len = Î”'.len + (k + 1) :=
begin
  cases le_iff_exists_add.mp (simplex_category.len_le_of_mono (infer_instance : mono i)) with k h,
  cases k,
  { exfalso,
    exact hi (simplex_category.ext Î” Î”' h), },
  { exact âŸ¨k, hâŸ©, },
end

variable (K)

@[simp, reassoc]
lemma map_mono_comp : map_mono K i â‰« map_mono K i' = map_mono K (i' â‰« i) :=
begin
  /- case where i : Î”' âŸ¶ Î” is the identity -/
  by_cases h1 : Î” = Î”',
  { unfreezingI { subst h1, },
    simp only [simplex_category.eq_id_of_mono i,
      comp_id, id_comp, map_mono_id K, eq_to_hom_refl], },
  /- case where i' : Î”'' âŸ¶ Î”' is the identity -/
  by_cases h2 : Î”' = Î”'',
  { unfreezingI { subst h2, },
    simp only [simplex_category.eq_id_of_mono i',
      comp_id, id_comp, map_mono_id K, eq_to_hom_refl], },
  /- then the RHS is always zero -/
  cases simplex_category_non_epi_mono i h1 with k hk,
  cases simplex_category_non_epi_mono i' h2 with k' hk',
  have eq : Î”.len = Î”''.len + (k+k'+2) := by { rw hk' at hk, linarith, },
  rw map_mono_eq_zero K (i' â‰« i) _ _, rotate,
  { by_contradiction,
    simpa only [self_eq_add_right,h ] using eq, },
  { by_contradiction,
    dsimp [is_dâ‚€] at h,
    simp only [h.left, add_right_inj] at eq,
    linarith, },
  /- in all cases, the LHS is also zero,
  either by definition, or because d â‰« d = 0 -/
  by_cases h3 : is_dâ‚€ i,
  { by_cases h4 : is_dâ‚€ i',
    { rw [map_mono_dâ‚€ K i h3, map_mono_dâ‚€ K i' h4,
        homological_complex.d_comp_d], },
    { simp only [map_mono_eq_zero K i' h2 h4, comp_zero], }, },
  { simp only [map_mono_eq_zero K i h1 h3, zero_comp], },
end

end termwise

/-- The simplicial morphism on the simplicial object `Î“â‚€.obj K` induced by
a morphism `Î”' â†’ Î”` in `simplex_category` is defined on each summand
associated to an `A : Î“_index_set Î”` in terms of the epi-mono factorisation
of `Î¸ â‰« A.e`. -/
def map (K : chain_complex C â„•) {Î”' Î” : simplex_category} (Î¸ : Î”' âŸ¶ Î”) :
  objâ‚‚ K Î” âŸ¶ objâ‚‚ K Î”' :=
sigma.desc (Î» A, termwise.map_mono K (image.Î¹ (Î¸ â‰« A.e)) â‰«
  (sigma.Î¹ (summand K Î”') (A.pull Î¸)))

variables {Î” Î”'}

@[reassoc]
lemma map_on_summandâ‚€ (A : splitting_index_set Î”) {Î¸ : Î”' âŸ¶ Î”}
  {e : Î”' âŸ¶ Î”''} {i : Î”'' âŸ¶ A.1} [epi e] [mono i]
  (fac : e â‰« i = Î¸ â‰« A.e) : (sigma.Î¹ (summand K Î”) A) â‰« map K Î¸ =
  termwise.map_mono K i â‰« sigma.Î¹ (summand K Î”') âŸ¨Î”'', âŸ¨e, by apply_instanceâŸ©âŸ© :=
begin
  simp only [map, colimit.Î¹_desc, cofan.mk_Î¹_app, simplex_category.splitting_index_set.pull],
  have h := simplex_category.image_eq fac,
  unfreezingI { subst h, },
  congr,
  { exact simplex_category.image_Î¹_eq fac, },
  { dsimp only [simplex_category.splitting_index_set.pull],
    congr,
    exact simplex_category.factor_thru_image_eq fac, },
end

@[reassoc]
lemma map_on_summandâ‚ (A : splitting_index_set Î”) (Î¸ : Î”' âŸ¶ Î”) :
  (sigma.Î¹ (summand K Î”) A) â‰« map K Î¸ =
  termwise.map_mono K (image.Î¹ (Î¸ â‰« A.e)) â‰« sigma.Î¹ (summand K _) (A.pull Î¸) :=
map_on_summandâ‚€ K A (A.fac_pull Î¸)

end obj

/-- The functor `Î“â‚€ : chain_complex C â„• â¥¤ simplicial_object C`, on objects. -/
@[simps]
def obj (K : chain_complex C â„•) : simplicial_object C :=
{ obj := Î» Î”, obj.objâ‚‚ K (unop Î”),
  map := Î» Î” Î”' Î¸, obj.map K Î¸.unop,
  map_id' := Î» Î”, begin
    ext A,
    cases A,
    have fac : A.e â‰« ğŸ™ A.1 = ğŸ™ Î”.unop â‰« A.e := by rw [comp_id, id_comp],
    erw [obj.map_on_summandâ‚€ K A fac, obj.termwise.map_mono_id, id_comp, comp_id],
    unfreezingI { rcases A with âŸ¨Î”', âŸ¨e, heâŸ©âŸ©, },
    congr,
  end,
  map_comp' := Î» Î”'' Î”' Î” Î¸' Î¸, begin
    ext A,
    cases A,
    have fac : Î¸.unop â‰« Î¸'.unop â‰« A.e = (Î¸' â‰« Î¸).unop â‰« A.e := by rw [unop_comp, assoc],
    rw [â† image.fac (Î¸'.unop â‰« A.e), â† assoc,
      â† image.fac (Î¸.unop â‰« factor_thru_image (Î¸'.unop â‰« A.e)), assoc] at fac,
    simpa only [obj.map_on_summandâ‚_assoc K A Î¸'.unop, obj.map_on_summandâ‚ K _ Î¸.unop,
      obj.termwise.map_mono_comp_assoc, obj.map_on_summandâ‚€ K A fac],
  end }

lemma splitting_map_eq_id (Î” : simplex_categoryáµ’áµ–) :
  (simplicial_object.splitting.map (Î“â‚€.obj K) (Î» (n : â„•), sigma.Î¹ (Î“â‚€.obj.summand K [n]) (splitting_index_set.id [n])) Î”)
    = ğŸ™ _ :=
begin
  ext A,
  discrete_cases,
  induction Î” using opposite.rec,
  induction Î” with n,
  dsimp,
  simp only [colimit.Î¹_desc, cofan.mk_Î¹_app, comp_id, Î“â‚€.obj_map],
  rw [Î“â‚€.obj.map_on_summandâ‚€ K
    (simplex_category.splitting_index_set.id A.1) (show A.e â‰« ğŸ™ _ = A.e â‰« ğŸ™ _, by refl),
    Î“â‚€.obj.termwise.map_mono_id, A.ext'],
  apply id_comp,
end

def splitting (K : chain_complex C â„•) :
  simplicial_object.splitting (Î“â‚€.obj K) :=
{ N := Î» n, K.X n,
  Î¹ := Î» n, sigma.Î¹ (Î“â‚€.obj.summand K [n]) (splitting_index_set.id [n]),
  is_iso' := Î» Î”, begin
    rw Î“â‚€.splitting_map_eq_id,
    apply is_iso.id,
  end, }

@[simp]
lemma splitting_iso_hom_eq_id (Î” : simplex_category): ((splitting K).iso Î”).hom = ğŸ™ _ :=
splitting_map_eq_id K (op Î”)

variables {Î” Î”'}

@[reassoc]
lemma obj.map_on_summand (A : splitting_index_set Î”) (Î¸ : Î”' âŸ¶ Î”)
  {e : Î”' âŸ¶ Î”''} {i : Î”'' âŸ¶ A.1} [epi e] [mono i]
  (fac : e â‰« i = Î¸ â‰« A.e) : (Î“â‚€.splitting K).Î¹_summand A â‰« (Î“â‚€.obj K).map Î¸.op =
  Î“â‚€.obj.termwise.map_mono K i â‰« (Î“â‚€.splitting K).Î¹_summand âŸ¨Î”'', e, infer_instanceâŸ© :=
begin
  dsimp only [simplicial_object.splitting.Î¹_summand,
    simplicial_object.splitting.Î¹_sum],
  simp only [assoc, Î“â‚€.splitting_iso_hom_eq_id, id_comp, comp_id],
  exact Î“â‚€.obj.map_on_summandâ‚€ K A fac,
end

@[reassoc]
lemma obj.map_on_summand' (A : splitting_index_set Î”) (Î¸ : Î”' âŸ¶ Î”) :
  (splitting K).Î¹_summand A â‰« (obj K).map Î¸.op =
    obj.termwise.map_mono K (image.Î¹ (Î¸ â‰« A.e)) â‰« (splitting K).Î¹_summand (A.pull Î¸) :=
by { apply obj.map_on_summand, apply image.fac, }

@[reassoc]
lemma obj.map_mono_on_summand_id (i : Î”' âŸ¶ Î”) [mono i] :
  (splitting K).Î¹_summand (splitting_index_set.id Î”) â‰« (obj K).map i.op =
  obj.termwise.map_mono K i â‰« (splitting K).Î¹_summand (splitting_index_set.id Î”') :=
obj.map_on_summand K (splitting_index_set.id Î”) i (show ğŸ™ _ â‰« i = i â‰« ğŸ™ _, by refl)

@[reassoc]
lemma obj.map_epi_on_summand_id (e : Î”' âŸ¶ Î”) [epi e] :
  (Î“â‚€.splitting K).Î¹_summand (splitting_index_set.id Î”) â‰« (Î“â‚€.obj K).map e.op =
    (Î“â‚€.splitting K).Î¹_summand âŸ¨Î”, âŸ¨e, infer_instanceâŸ©âŸ© :=
by simpa only [Î“â‚€.obj.map_on_summand K (splitting_index_set.id Î”) e
    (rfl : e â‰« ğŸ™ Î” = e â‰« ğŸ™ Î”), Î“â‚€.obj.termwise.map_mono_id] using id_comp _

/-- The functor `Î“â‚€ : chain_complex C â„• â¥¤ simplicial_object C`, on morphisms. -/
@[simps]
def map {K K' : chain_complex C â„•} (f : K âŸ¶ K') : obj K âŸ¶ obj K' :=
{ app := Î» Î”, (Î“â‚€.splitting K).desc Î” (Î» A, f.f A.1.len â‰« (Î“â‚€.splitting K').Î¹_summand A),
  naturality' := Î» Î”' Î” Î¸, begin
    apply (Î“â‚€.splitting K).hom_ext',
    intro A,
    erw [(splitting K).Î¹_desc_assoc, obj.map_on_summand'_assoc K _ Î¸.unop,
      (splitting K).Î¹_desc, assoc, obj.map_on_summand' K' _ Î¸.unop,
      obj.termwise.map_mono_naturality_assoc],
  end, }

end Î“â‚€

/-- The functor `Î“â‚€ : chain_complex C â„• â¥¤ simplicial_object C`, which is
the inverse functor of the Dold-Kan equivalence in the category abelian
categories, or more generally pseudoabelian categories. -/
@[simps]
def Î“â‚€ : chain_complex C â„• â¥¤ simplicial_object C :=
{ obj := Î“â‚€.obj,
  map := Î» _ _, Î“â‚€.map,
  map_id' := Î» K, begin
    apply (Î“â‚€.splitting K).hom_ext,
    intro n,
    simpa only [simplicial_object.splitting.Ï†, â† simplicial_object.splitting.Î¹_summand_id,
      Î“â‚€.map_app, homological_complex.id_f, nat_trans.id_app, comp_id,
      (Î“â‚€.splitting K).Î¹_desc (op [n])] using id_comp _,
  end,
  map_comp' := Î» K K' K'' f f', begin
    apply (Î“â‚€.splitting K).hom_ext,
    intro n,
    simp only [simplicial_object.splitting.Ï†, â† simplicial_object.splitting.Î¹_summand_id,
      Î“â‚€.map_app, nat_trans.comp_app, assoc, homological_complex.comp_f,
      (Î“â‚€.splitting K).Î¹_desc (op [n]), (Î“â‚€.splitting K).Î¹_desc_assoc (op [n]),
      (Î“â‚€.splitting K').Î¹_desc (op [n])],
  end, }

/-- The extension of `Î“â‚€ : chain_complex C â„• â¥¤ simplicial_object C`
on the idempotent completions. It shall be an equivalence of categories
for any additive category `C`. -/
@[simps]
def Î“â‚‚ : karoubi (chain_complex C â„•) â¥¤ karoubi (simplicial_object C) :=
(category_theory.idempotents.functor_extensionâ‚‚ _ _).obj Î“â‚€

end dold_kan

end algebraic_topology
