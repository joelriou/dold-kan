/-
Copyright (c) 2022 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/

import for_mathlib.dold_kan.functor_n
import for_mathlib.dold_kan.functor_gamma
import for_mathlib.dold_kan.degeneracies
import for_mathlib.idempotents.nat_trans

noncomputable theory

open category_theory
open category_theory.category
open category_theory.limits
open category_theory.idempotents
open simplex_category
open opposite
open simplicial_object
open_locale simplicial dold_kan

namespace algebraic_topology

namespace dold_kan

variables {C : Type*} [category C] [additive_category C]

@[reassoc]
lemma P_infty_on_Î“â‚€_splitting_summand_eq_zero
  (K : chain_complex C â„•) (n : â„•) {A : splitting.index_set (op [n])}
  (hA : Â¬ A.eq_id) :
  (Î“â‚€.splitting K).Î¹_summand A â‰« (P_infty : K[Î“â‚€.obj K] âŸ¶ _).f n = 0 :=
P_infty_on_splitting_eq_zero (Î“â‚€.splitting K) A hA

def higher_faces_vanish.on_Î“â‚€_summand_id (K : chain_complex C â„•) (n : â„•) :
  higher_faces_vanish (n+1) ((Î“â‚€.splitting K).Î¹_summand (splitting.index_set.id (op [n+1]))) :=
begin
  intros j hj,
  have eq := Î“â‚€.obj.map_mono_on_summand_id K (simplex_category.Î´ j.succ),
  rw [Î“â‚€.obj.termwise.map_mono_eq_zero K, zero_comp] at eq, rotate,
  { intro h,
    exact (nat.succ_ne_self n) (congr_arg simplex_category.len h), },
  { intro h,
    simp only [is_dâ‚€.iff] at h,
    exact fin.succ_ne_zero j h, },
  exact eq,
end

@[simp, reassoc]
lemma P_infty_on_Î“â‚€_splitting_summand_eq_self
  (K : chain_complex C â„•) {n : â„•} :
  (Î“â‚€.splitting K).Î¹_summand (splitting.index_set.id (op [n])) â‰« (P_infty : K[Î“â‚€.obj K] âŸ¶ _).f n =
    (Î“â‚€.splitting K).Î¹_summand (splitting.index_set.id (op [n])) :=
begin
  rw P_infty_f,
  cases n,
  { simpa only [P_f_0_eq] using comp_id _, },
  { exact (higher_faces_vanish.on_Î“â‚€_summand_id K n).comp_P_eq_self, },
end

namespace Nâ‚Î“â‚€

def hom_app_f_f_termwise (K : chain_complex C â„•) (n : â„•) (A : splitting.index_set (op [n])) :
  Î“â‚€.obj.summand K (op [n]) A âŸ¶ K.X n :=
begin
  by_cases A.eq_id,
  { dsimp at h,
    subst h,
    exact ğŸ™ _, },
  { exact 0, }
end

@[simp]
lemma hom_app_f_f_termwise_eq_id (K : chain_complex C â„•) (n : â„•) :
hom_app_f_f_termwise K n (splitting.index_set.id (op [n])) = ğŸ™ _ :=
begin
  dsimp [hom_app_f_f_termwise],
  simp only [eq_self_iff_true, if_true],
end

lemma hom_app_f_f_termwise_eq_zero (K : chain_complex C â„•) (n : â„•)
  {A : splitting.index_set (op [n])} (h : Â¬ A.eq_id) :
hom_app_f_f_termwise K n A = 0 :=
begin
  dsimp [hom_app_f_f_termwise],
  rw dif_neg,
  exact h,
end

@[simp]
def hom_app_f_f (K : chain_complex C â„•) (n : â„•) :
  (Î“â‚€.obj K) _[n] âŸ¶ K.X n :=
(Î“â‚€.splitting K).desc (op [n]) (hom_app_f_f_termwise K n)

@[simp, reassoc]
lemma Î¹_hom_app_f_f (K : chain_complex C â„•) (n : â„•) (A : splitting.index_set (op [n])) :
  (Î“â‚€.splitting K).Î¹_summand A â‰« hom_app_f_f K n = hom_app_f_f_termwise K n A :=
(Î“â‚€.splitting K).Î¹_desc (op [n]) (hom_app_f_f_termwise K n) A

@[reassoc]
lemma Î¹_id_d (K : chain_complex C â„•) (i j : â„•) (hij : j+1 = i) :
  (Î“â‚€.splitting K).Î¹_summand (splitting.index_set.id (op [i])) â‰«
    ((Î“â‚€ â‹™ Nâ‚).obj K).X.d i j =
  K.d i j â‰« (Î“â‚€.splitting K).Î¹_summand (splitting.index_set.id (op [j])) :=
begin
  subst hij,
  dsimp,
  simp only [alternating_face_map_complex.obj_d_eq, preadditive.comp_sum],
  rw finset.sum_eq_single (0 : fin (j+2)), rotate,
  { intros b h hb,
    erw [preadditive.comp_zsmul, Î“â‚€.obj.map_mono_on_summand_id,
      Î“â‚€.obj.termwise.map_mono_eq_zero, zero_comp, zsmul_zero],
    { intro hj,
      exact (nat.succ_ne_self j) (congr_arg simplex_category.len hj), },
    { simpa only [is_dâ‚€.iff] using hb, }, },
  { intro h,
    exfalso,
    simpa only [finset.mem_univ, not_true] using h, },
  erw [fin.coe_zero, pow_zero, one_zsmul,
    Î“â‚€.obj.map_mono_on_summand_id K (simplex_category.Î´ (0 : fin (j+2))),
    Î“â‚€.obj.termwise.map_mono_dâ‚€'],
end

lemma Î¹_d_hom_app_eq_zero.term_is_zero (K : chain_complex C â„•) (j : â„•)
  {A : splitting.index_set (op [j+1])} (hA : Â¬ A.eq_id) (b : fin (j+2))
  (hb : Â¬ is_iso (simplex_category.Î´ b â‰« A.e)):
  (-1 : â„¤) ^ (b : â„•) â€¢ (Î“â‚€.splitting K).Î¹_summand A â‰«
    (Î“â‚€.obj K).Î´ b â‰« hom_app_f_f K j = 0 :=
begin
  erw Î“â‚€.obj.map_on_summand'_assoc K A (simplex_category.Î´ b).op,
  simp only [hom_app_f_f, splitting.Î¹_desc],
  rw [hom_app_f_f_termwise_eq_zero, comp_zero, zsmul_zero],
  intro h,
  apply hb,
  have h' : mono (Î´ b â‰« A.e),
  { rw splitting.index_set.eq_id_iff_mono at h,
    change mono (factor_thru_image (Î´ b â‰« A.e)) at h,
    rw â† image.fac (Î´ b â‰« A.e),
    haveI := h,
    apply mono_comp, },
  apply is_iso_of_bijective,
  rw fintype.bijective_iff_injective_and_card,
  split,
  { simpa only [simplex_category.mono_iff_injective] using h', },
  { simp only [len_mk, fintype.card_fin, add_left_inj],
    apply le_antisymm (len_le_of_mono h'),
    simpa only [splitting.index_set.eq_id_iff_len_le, unop_op, len_mk, not_le,
      nat.lt_succ_iff] using hA, },
end

lemma fin.is_succ_of_ne_zero {j : â„•} (x : fin (j+1)) (hx : x â‰  0) :
  âˆƒ (y : fin j), x = y.succ :=
âŸ¨x.pred hx, (fin.succ_pred _ _).symmâŸ©

lemma simplex_category.Î´_comp_Ïƒ_of_lt' {k : â„•} (b : fin (k+3)) (i : fin (k+2)) (h : (b : â„•) < i) :
  âˆƒ (j : fin (k+1)) (a : fin (k+2)) (hj : i = j.succ) (ha : b = a.cast_succ),
    Î´ b â‰« Ïƒ i = Ïƒ j â‰« Î´ a :=
begin
  have hi : i â‰  0,
  { intro hi,
    simpa only [hi, fin.coe_zero, not_lt_zero'] using h, },
  cases fin.is_succ_of_ne_zero i hi with j hj,
  subst hj,
  let a := fin.cast_pred b,
  have ha : a.cast_succ = b,
  { apply fin.cast_succ_cast_pred,
    rw fin.lt_iff_coe_lt_coe,
    apply h.trans,
    simpa only [fin.coe_succ, fin.coe_last, add_lt_add_iff_right] using j.is_lt, },
  have h' : a â‰¤ j.cast_succ,
  { dsimp [a],
    simpa only [â† ha, fin.le_iff_coe_le_coe, fin.cast_pred_cast_succ, fin.coe_cast_succ,
      â† nat.lt_succ_iff, fin.coe_succ] using h, },
  exact âŸ¨j, a, rfl, ha.symm, by simpa only [â† ha] using simplex_category.Î´_comp_Ïƒ_of_le h'âŸ©,
end

lemma simplex_category.Î´_comp_Ïƒ_of_le' {k : â„•} (b : fin (k+3)) (i : fin (k+2)) (h : (i : â„•)+2 â‰¤ b) :
  âˆƒ (j : fin (k+1)) (a : fin (k+2)) (hj : i = j.cast_succ) (ha : b = a.succ),
    Î´ b â‰« Ïƒ i = Ïƒ j â‰« Î´ a :=
begin
  have hb : b â‰  0,
  { intro hb,
    simpa only [hb, fin.coe_zero, le_zero_iff] using h, },
  cases fin.is_succ_of_ne_zero b hb with a ha,
  let j := i.cast_pred,
  have hj : j.cast_succ = i,
  { apply fin.cast_succ_cast_pred,
    rw [fin.lt_iff_coe_lt_coe, â† nat.succ_le_iff, â† nat.succ_le_succ_iff],
    apply h.trans,
    simpa only [â† nat.lt_succ_iff] using b.is_lt, },
  have h' : j.cast_succ < a,
  { rw [hj, fin.lt_iff_coe_lt_coe, â† nat.succ_lt_succ_iff, â† nat.succ_le_iff],
    simpa only [ha, fin.coe_succ] using h, },
  exact âŸ¨j, a, hj.symm, ha, by simpa only [ha, â† hj] using simplex_category.Î´_comp_Ïƒ_of_gt h'âŸ©,
end

lemma Î¹_d_hom_app_eq_zero (K : chain_complex C â„•) (i j : â„•) (hij : j+1=i)
  {A : splitting.index_set (op [i])} (hA : Â¬ A.eq_id) :
  (Î“â‚€.splitting K).Î¹_summand A â‰« ((Î“â‚€ â‹™ Nâ‚).obj K).X.d i j â‰« hom_app_f_f K j = 0 :=
begin
  subst hij,
  dsimp only [functor.comp_obj, Nâ‚],
  simp only [alternating_face_map_complex.obj_d_eq, preadditive.sum_comp,
    preadditive.comp_sum, preadditive.zsmul_comp, preadditive.comp_zsmul],
  by_cases hA' : A.1.unop.len = j,
  { rcases A with âŸ¨Î”, e, heâŸ©,
    induction Î” using opposite.rec,
    induction Î” using simplex_category.rec with k,
    dsimp at hA',
    subst hA',
    haveI := he,
    cases simplex_category.eq_Ïƒ_of_epi e with i hi,
    unfreezingI { subst hi, },
    rw fintype.sum_eq_add (fin.cast_succ i) i.succ, rotate,
    { intro h,
      simpa only [fin.ext_iff, fin.coe_cast_succ, fin.coe_succ,
        self_eq_add_right, nat.one_ne_zero] using h, },
    { intros b hb,
      apply Î¹_d_hom_app_eq_zero.term_is_zero K k hA,
      intro h,
      change is_iso (simplex_category.Î´ b â‰« simplex_category.Ïƒ i) at h,
      by_cases hbi : (b : â„•)<i,
      { unfreezingI { cases k, },
        { exfalso,
          fin_cases i,
          simpa only [this, fin.coe_fin_one, not_lt_zero'] using hbi, },
        { rcases simplex_category.Î´_comp_Ïƒ_of_lt' b i hbi with âŸ¨j, a, hj, ha, eqâŸ©,
          rw eq at h,
          haveI := h,
          simpa only [len_mk, add_le_iff_nonpos_right, le_zero_iff] using
            len_le_of_epi (epi_of_epi (Ïƒ j) (Î´ a)), }, },
      { have hbi' : (i : â„•)+2 â‰¤ b,
        { simp only [not_lt] at hbi,
          cases nat.le.dest hbi with t ht,
          suffices : 2 â‰¤ t,
          { linarith, },
          by_contra' ht' : _,
          let t' : fin 2 := âŸ¨t, ht'âŸ©,
          fin_cases t',
          { apply hb.1,
            symmetry,
            simp only [t', fin.ext_iff, fin.coe_mk, fin.coe_zero] at this,
            simpa only [this, add_zero, fin.ext_iff] using ht, },
          { apply hb.2,
            symmetry,
            simp only [fin.ext_iff, fin.coe_mk, fin.coe_one] at this,
            simpa only [this, fin.ext_iff, fin.coe_succ] using ht, }, },
        unfreezingI { cases k, },
        { have h' := b.is_lt, linarith, },
        { rcases simplex_category.Î´_comp_Ïƒ_of_le' b i hbi' with âŸ¨j, a, hj, ha, eqâŸ©,
          rw eq at h,
          haveI := h,
          simpa only [len_mk, add_le_iff_nonpos_right, le_zero_iff] using
              len_le_of_epi (epi_of_epi (Ïƒ j) (Î´ a)), }, }, },
    { let A : splitting.index_set (op [k+1]) := âŸ¨op [k], âŸ¨Ïƒ i, heâŸ©âŸ©,
      erw [Î“â‚€.obj.map_on_summand_assoc K A
        (simplex_category.Î´ i.succ).op (_ : ğŸ™ _ â‰« ğŸ™ _ = _),
        Î“â‚€.obj.map_on_summand_assoc K A
        (simplex_category.Î´ (fin.cast_succ i)).op (_ : ğŸ™ _ â‰« ğŸ™ _ = _),
        fin.coe_cast_succ, fin.coe_succ, pow_succ, neg_mul, one_mul, neg_smul, add_right_neg];
      symmetry; rw [id_comp, quiver.hom.unop_op],
      exacts [Î´_comp_Ïƒ_self, Î´_comp_Ïƒ_succ], }, },
  { rw finset.sum_eq_zero,
    intros b h,
    apply Î¹_d_hom_app_eq_zero.term_is_zero K j hA b,
    introI hb,
    simp only [splitting.index_set.eq_id_iff_len_le, unop_op, len_mk, not_le] at hA,
    have hA'' : j < _ := lt_of_le_of_ne (len_le_of_mono (infer_instance : mono (Î´ b â‰« A.e)))
      (ne_comm.mp hA'),
    rw â† nat.succ_le_iff at hA'',
    simpa only [lt_self_iff_false] using lt_of_le_of_lt hA'' hA, },
end

@[simps]
def hom_app (K : chain_complex C â„•) : (Î“â‚€ â‹™ Nâ‚).obj K âŸ¶ (to_karoubi (chain_complex C â„•)).obj K :=
{ f :=
  { f := Î» n, hom_app_f_f K n,
    comm' := Î» i j hij, begin
      apply (Î“â‚€.splitting K).hom_ext',
      intro A,
      by_cases A.eq_id,
      { dsimp at h,
        subst h,
        simp only [Î¹_id_d_assoc K i j hij, hom_app_f_f, splitting.Î¹_desc,
          splitting.Î¹_desc_assoc, hom_app_f_f_termwise_eq_id],
        dsimp,
        erw [id_comp, comp_id], },
      { rw [Î¹_d_hom_app_eq_zero K i j hij h, hom_app_f_f,
          splitting.Î¹_desc_assoc, hom_app_f_f_termwise_eq_zero K i h, zero_comp], },
    end, },
  comm := begin
    ext n : 2,
    apply (Î“â‚€.splitting K).hom_ext',
    intro A,
    dsimp,
    rw simplicial_object.splitting.Î¹_desc,
    by_cases A.eq_id,
    { dsimp at h,
      subst h,
      simp only [hom_app_f_f_termwise_eq_id, P_infty_on_Î“â‚€_splitting_summand_eq_self_assoc,
        splitting.Î¹_desc, comp_id], },
    { simp only [hom_app_f_f_termwise_eq_zero K n h, zero_comp,
        P_infty_on_Î“â‚€_splitting_summand_eq_zero_assoc K n h], },
  end, }

@[simps]
def hom : Î“â‚€ â‹™ Nâ‚ âŸ¶ to_karoubi (chain_complex C â„•) :=
{ app := hom_app,
  naturality' := Î» K L f, begin
    ext n : 3,
    apply (Î“â‚€.splitting K).hom_ext',
    intro A,
    dsimp,
    simp only [Nâ‚_map_f, assoc, karoubi.comp, homological_complex.comp_f,
      alternating_face_map_complex.map_f, Î“â‚€.map_app, hom_app_f_f, hom_app_f_f_2,
      to_karoubi_map_f, splitting.Î¹_desc_assoc],
    by_cases A.eq_id,
    { dsimp at h,
      subst h,
      simp only [P_infty_on_Î“â‚€_splitting_summand_eq_self_assoc, splitting.Î¹_desc_assoc,
        assoc, splitting.Î¹_desc, hom_app_f_f_termwise_eq_id],
      dsimp [splitting.index_set.id],
      erw [comp_id, id_comp], },
    { simp only [P_infty_on_Î“â‚€_splitting_summand_eq_zero_assoc K n h,
        hom_app_f_f_termwise_eq_zero K n h, zero_comp], },
  end }

@[simp]
def inv_app_f_f (K : chain_complex C â„•) (n : â„•) : K.X n âŸ¶ (Î“â‚€.obj K) _[n] :=
(Î“â‚€.splitting K).Î¹_summand (splitting.index_set.id (op [n]))

@[simps]
def inv_app (K : chain_complex C â„•) : (to_karoubi (chain_complex C â„•)).obj K âŸ¶ (Î“â‚€ â‹™ Nâ‚).obj K :=
{ f :=
  { f := Î» n, inv_app_f_f K n,
    comm' := Î» i j hij, Î¹_id_d K i j hij, },
  comm := by tidy, }

@[simps]
def inv : to_karoubi (chain_complex C â„•) âŸ¶ Î“â‚€ â‹™ Nâ‚ :=
{ app := inv_app,
  naturality' := Î» X Y f, begin
    ext n : 3,
    dsimp,
    simpa only [to_karoubi_map_f, karoubi.comp, homological_complex.comp_f,
      inv_app_f_f, inv_app_f_f_2, Nâ‚_map_f, alternating_face_map_complex.map_f,
      Î“â‚€.map_app, P_infty_on_Î“â‚€_splitting_summand_eq_self_assoc,
      splitting.Î¹_desc],
  end, }

end Nâ‚Î“â‚€

@[simps]
def Nâ‚Î“â‚€ : Î“â‚€ â‹™ Nâ‚ â‰… to_karoubi (chain_complex C â„•) :=
{ hom := Nâ‚Î“â‚€.hom,
  inv := Nâ‚Î“â‚€.inv,
  hom_inv_id' := begin
    ext K n : 5,
    apply (Î“â‚€.splitting K).hom_ext',
    intro A,
    simp only [nat_trans.comp_app, Nâ‚Î“â‚€.hom_app_2, Nâ‚Î“â‚€.inv_app_2,
      karoubi.comp, homological_complex.comp_f, Nâ‚Î“â‚€.hom_app_f_f,
      Nâ‚Î“â‚€.hom_app_f_f_2, Nâ‚Î“â‚€.inv_app_f_f, Nâ‚Î“â‚€.inv_app_f_f_2,
      nat_trans.id_app, karoubi.id_eq, splitting.Î¹_desc_assoc],
    by_cases A.eq_id,
    { dsimp at h,
      subst h,
      dsimp,
      simpa only [Nâ‚Î“â‚€.hom_app_f_f_termwise_eq_id,
        P_infty_on_Î“â‚€_splitting_summand_eq_self] using id_comp _, },
    { dsimp,
      simp only [Nâ‚Î“â‚€.hom_app_f_f_termwise_eq_zero K n h,
        P_infty_on_Î“â‚€_splitting_summand_eq_zero K n h, zero_comp], },
  end,
  inv_hom_id' := by tidy, }

lemma Nâ‚‚Î“â‚‚_to_karoubi : to_karoubi (chain_complex C â„•) â‹™ Î“â‚‚ â‹™ Nâ‚‚ = Î“â‚€ â‹™ Nâ‚ :=
begin
  have h := functor.congr_obj (functor_extensionâ‚‚_comp_whiskering_left_to_karoubi
    (chain_complex C â„•) (simplicial_object C)) Î“â‚€,
  have h' := functor.congr_obj (functor_extensionâ‚_comp_whiskering_left_to_karoubi
    (simplicial_object C) (chain_complex C â„•)) Nâ‚,
  dsimp [Nâ‚‚, Î“â‚‚, functor_extensionâ‚] at h h' âŠ¢,
  rw [â† functor.assoc_eq, h, functor.assoc_eq, h'],
end

@[simps]
def Nâ‚‚Î“â‚‚_to_karoubi_iso : to_karoubi (chain_complex C â„•) â‹™ Î“â‚‚ â‹™ Nâ‚‚ â‰… Î“â‚€ â‹™ Nâ‚ :=
eq_to_iso (Nâ‚‚Î“â‚‚_to_karoubi)

@[simps]
def Nâ‚‚Î“â‚‚ : Î“â‚‚ â‹™ Nâ‚‚ â‰… ğŸ­ (karoubi (chain_complex C â„•)) :=
(whiskering_left_to_karoubi_iso_equiv (Î“â‚‚ â‹™ Nâ‚‚) (ğŸ­ (karoubi (chain_complex C â„•)))).inv_fun
  (Nâ‚‚Î“â‚‚_to_karoubi_iso.trans Nâ‚Î“â‚€)

lemma Nâ‚‚Î“â‚‚_compatible_with_Nâ‚Î“â‚€ (K: chain_complex C â„•) :
  Nâ‚‚Î“â‚‚.hom.app ((to_karoubi _).obj K) = Nâ‚‚Î“â‚‚_to_karoubi_iso.hom.app K â‰« Nâ‚Î“â‚€.hom.app K :=
begin
  dsimp only [Nâ‚‚Î“â‚‚, Nâ‚Î“â‚€, whiskering_left_to_karoubi_iso_equiv],
  rw whiskering_left_to_karoubi_hom_equiv_inv_fun_compat,
  dsimp only [iso.trans, eq_to_iso],
  apply nat_trans.comp_app,
end

end dold_kan

end algebraic_topology
