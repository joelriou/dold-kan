/-
Copyright (c) 2022 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/

import category_theory.idempotents.functor_extension
import for_mathlib.functor_misc

/-!
# Extension of functors to the idempotent completion

In this file, we obtain an equivalence of categories
`karoubi_universal‚ÇÅ C D : (C ‚•§ karoubi D) ‚âå (karoubi C ‚•§ karoubi D)` for
all categories `C` and `D`. The key construction is `functor_extension‚ÇÅ`
which extends a functor `C ‚•§ karoubi D` to a functor `karoubi C ‚•§ karoubi D`.

TODO : If `D` is idempotent complete, we also have
`karoubi_universal C D : C ‚•§ D ‚âå karoubi C ‚•§ D`.

-/

open category_theory.category
open category_theory.idempotents.karoubi

namespace category_theory

namespace idempotents

variables (C D E : Type*) [category C] [category D] [category E]

namespace karoubi_universal‚ÇÅ

@[simps]
def counit_iso :
  (whiskering_left C (karoubi C) (karoubi D)).obj (to_karoubi C) ‚ãô
    functor_extension‚ÇÅ C D ‚âÖ ùü≠ _ :=
nat_iso.of_components (Œª G,
{ hom :=
  { app := Œª P,
    { f := (G.map (decomp_id_p P)).f,
      comm := begin
        have eq : P.decomp_id_p = (to_karoubi C).map P.p ‚â´ P.decomp_id_p ‚â´ ùüô _,
        { simp only [P.idem, decomp_id_p_f, to_karoubi_map_f, id_eq, comp, hom_ext], },
        simpa only [hom_ext, G.map_comp, G.map_id] using G.congr_map eq,
      end },
      naturality' := Œª P Q f, begin
        ext,
        simpa only [hom_ext, G.map_comp] using (G.congr_map (decomp_id_p_naturality f)).symm,
      end },
  inv :=
  { app := Œª P,
    { f := (G.map (decomp_id_i P)).f,
      comm := begin
        have eq : P.decomp_id_i = ùüô _ ‚â´ P.decomp_id_i ‚â´ (to_karoubi C).map P.p,
        { simp only [P.idem, decomp_id_i_f, to_karoubi_map_f, id_eq, comp, hom_ext], },
        simpa only [hom_ext, G.map_comp, G.map_id] using (G.congr_map eq),
      end, },
    naturality' := Œª P Q f, begin
      ext,
      simpa only [hom_ext, G.map_comp] using G.congr_map (decomp_id_i_naturality f),
    end },
  hom_inv_id' := begin
    ext P,
    simpa only [hom_ext, G.map_comp, G.map_id] using G.congr_map P.decomp_p.symm,
  end,
  inv_hom_id' := begin
    ext P,
    simpa only [hom_ext, G.map_comp, G.map_id] using G.congr_map P.decomp_id.symm,
  end, })
begin
  intros G G' œÜ,
  ext P,
  dsimp,
  simp only [nat_trans_eq œÜ P, comp, functor_extension‚ÇÅ.map_app_f,
    functor.comp_map, whisker_left_app],
  rw [P.decomp_p, G.map_comp, comp, assoc, assoc],
  refl,
end

end karoubi_universal‚ÇÅ

@[simps]
def karoubi_universal‚ÇÅ : (C ‚•§ karoubi D) ‚âå (karoubi C ‚•§ karoubi D) :=
{ functor := functor_extension‚ÇÅ C D,
  inverse := (whiskering_left C (karoubi C) (karoubi D)).obj (to_karoubi C),
  unit_iso := eq_to_iso (functor_extension‚ÇÅ_comp_whiskering_left_to_karoubi C D).symm,
  counit_iso := karoubi_universal‚ÇÅ.counit_iso C D,
  functor_unit_iso_comp' := Œª F, begin
    ext P,
    simpa only [eq_to_iso.hom, eq_to_hom_app, eq_to_hom_map, eq_to_hom_refl, id_comp]
      using F.congr_map P.idem,
  end, }

lemma functor_extension‚ÇÅ_comp
  (F : C ‚•§ karoubi D) (G : D ‚•§ karoubi E) :
  (functor_extension‚ÇÅ C E).obj (F ‚ãô (functor_extension‚ÇÅ D E).obj G) =
  (functor_extension‚ÇÅ C D).obj F ‚ãô (functor_extension‚ÇÅ D E).obj G :=
begin
  apply functor.ext,
  { intros X Y f,
    erw [id_comp, comp_id],
    refl, },
  { intro P,
    ext,
    { dsimp,
      erw [comp_id, id_comp], },
    { refl, }, }
end

@[simps]
def functor_extension‚ÇÇ : (C ‚•§ D) ‚•§ (karoubi C ‚•§ karoubi D) :=
(whiskering_right C D (karoubi D)).obj (to_karoubi D) ‚ãô functor_extension‚ÇÅ C D

lemma functor_extension‚ÇÇ_comp_whiskering_left_to_karoubi :
  functor_extension‚ÇÇ C D ‚ãô
    (whiskering_left C (karoubi C) (karoubi D)).obj (to_karoubi C) =
  (whiskering_right C D (karoubi D)).obj (to_karoubi D) :=
by simp only [functor_extension‚ÇÇ, functor.assoc_eq,
  functor_extension‚ÇÅ_comp_whiskering_left_to_karoubi, functor.comp_id]

section is_idempotent_complete

variable [is_idempotent_complete D]

noncomputable instance : is_equivalence (to_karoubi D) := to_karoubi_is_equivalence D

@[simps]
noncomputable def karoubi_universal‚ÇÇ : (C ‚•§ D) ‚âå
  (karoubi C ‚•§ karoubi D) :=
(equivalence.congr_right (to_karoubi D).as_equivalence).trans
    (karoubi_universal‚ÇÅ C D)

lemma karoubi_universal‚ÇÇ_functor_eq :
  (karoubi_universal‚ÇÇ C D).functor = functor_extension‚ÇÇ C D := rfl

noncomputable instance : is_equivalence (functor_extension‚ÇÇ C D) :=
by { rw ‚Üê karoubi_universal‚ÇÇ_functor_eq, apply_instance, }

@[simps]
noncomputable def functor_extension :
  (C ‚•§ D) ‚•§ (karoubi C ‚•§ D) :=
functor_extension‚ÇÇ C D ‚ãô (whiskering_right (karoubi C) (karoubi D) D).obj
    (to_karoubi_is_equivalence D).inverse

@[simps]
noncomputable def karoubi_universal : (C ‚•§ D) ‚âå (karoubi C ‚•§ D) :=
(karoubi_universal‚ÇÇ C D).trans (equivalence.congr_right (to_karoubi D).as_equivalence.symm)

lemma karoubi_universal_functor_eq :
  (karoubi_universal C D).functor = functor_extension C D := rfl

noncomputable instance : is_equivalence (functor_extension C D) :=
by { rw ‚Üê karoubi_universal_functor_eq, apply_instance, }

noncomputable instance : is_equivalence ((whiskering_left C (karoubi C) D).obj (to_karoubi C)) :=
begin
  let F‚ÇÅ := ((whiskering_left C (karoubi C) D).obj (to_karoubi C)),
  let F‚ÇÇ := ((whiskering_right C _ _).obj (to_karoubi D) ‚ãô
    (whiskering_right C _ _).obj (to_karoubi D).inv),
  apply is_equivalence.cancel_comp_right F‚ÇÅ F‚ÇÇ,
  { exact is_equivalence.of_equivalence
      (@equivalence.congr_right _ _ _ _ C _
      ((to_karoubi D).as_equivalence.trans (to_karoubi D).as_equivalence.symm)), },
  { rw [show F‚ÇÅ ‚ãô F‚ÇÇ = (karoubi_universal C D).inverse, by refl],
    apply_instance, }
end

end is_idempotent_complete

end idempotents

end category_theory
