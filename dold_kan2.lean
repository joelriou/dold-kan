/-
Copyright (c) 2022 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Author: JoÃ«l Riou
-/

import category_theory.functor_ext

import algebra.homology.homological_complex
import algebra.homology.homotopy
import algebra.big_operators.basic
import algebraic_topology.alternating_face_map_complex
import category_theory.pseudoabelian.simplicial_object
import category_theory.pseudoabelian.homological_complex

import dold_kan1

/-!

Goal : 
* show that a morphism of simplicial objects is an isomorphisms if and only if it
induces an isomorphism on normalized Moore complexes,
this is `normalized_Moore_complex_reflects_iso`

-/

open category_theory
open category_theory.limits
open category_theory.subobject
open category_theory.preadditive
open category_theory.simplicial_object
open category_theory.category
open category_theory.pseudoabelian
open homology
open opposite

open_locale big_operators
open_locale simplicial

noncomputable theory

universes v

namespace algebraic_topology

namespace dold_kan

variables {C : Type*} [category.{v} C] [preadditive C]

lemma hÏƒ_naturality (q n : â„•) {X Y : simplicial_object C} (f : X âŸ¶ Y) :
  (f.app (op (simplex_category.mk n)) â‰« hÏƒ q n : X _[n] âŸ¶ Y _[n+1]) =
  hÏƒ q n â‰« f.app (op (simplex_category.mk (n+1))) :=
begin
  by_cases hqn : n<q; unfold hÏƒ; split_ifs,
  { simp only [zero_comp, comp_zero], },
  { simp only [zsmul_comp, comp_zsmul],
    congr' 1,
    erw f.naturality,
    refl, },
end

lemma hÏƒ'_naturality (q : â„•) (ij : homotopy.set_of_cs c)
  {X Y : simplicial_object C} (f : X âŸ¶ Y) :
  (f.app (op (simplex_category.mk ij.val.1))) â‰« hÏƒ' q ij =
  hÏƒ' q ij â‰« f.app (op (simplex_category.mk ij.val.2)) :=
begin
  simp only [hÏƒ', â† assoc, hÏƒ_naturality],
  have eq := f.naturality (eq_to_hom (show op [ij.val.1+1] = op [ij.val.2], by { congr, exact ij.property, })),
  simp only [eq_to_hom_map] at eq,
  simp only [assoc, eq],
end

/-- For each q, HÏƒ q is a natural transformation. -/
def nat_trans_HÏƒ (q : â„•) : ((alternating_face_map_complex C) âŸ¶
  (alternating_face_map_complex C)) :=
{ app := Î» _, HÏƒ q,
  naturality' := Î» X Y f,
  begin
    unfold HÏƒ,
    rw [homotopy.comp_null_homotopic_map, homotopy.null_homotopic_map_comp],
    congr,
    rw [homotopy.comp_prehomotopy, homotopy.prehomotopy_comp],
    simp only [hÏƒ'_naturality, chain_complex.of_hom_f,
      alternating_face_map_complex_map, alternating_face_map_complex.map],
  end }

/-- For each q, P q is a natural transformation. -/
def nat_trans_P (q : â„•) : ((alternating_face_map_complex C) âŸ¶
  (alternating_face_map_complex C)) :=
{ app := Î» _, P q,
  naturality' := Î» X Y f,
  begin
    induction q with q hq,
    { simp only [P, id_comp, comp_id], },
    { unfold P,
      simp only [add_comp, comp_add, assoc, comp_id],
      rw hq,
      congr' 1,
      rw [â† assoc, hq, assoc],
      congr' 1,
      exact (nat_trans_HÏƒ q).naturality' f, }
  end }

lemma P_termwise_naturality (q n : â„•) {X Y : simplicial_object C} (f : X âŸ¶ Y) :
   f.app (op [n]) â‰« (P q).f n = (P q).f n â‰« f.app (op [n]) :=
homological_complex.congr_hom ((nat_trans_P q).naturality f) n

lemma P_infty_termwise_naturality (n : â„•) {X Y : simplicial_object C} (f : X âŸ¶ Y) :
   f.app (op [n]) â‰« P_infty.f n = P_infty.f n â‰« f.app (op [n]) :=
by { dsimp [P_infty], rw P_termwise_naturality, }

variables (C)

@[simps]
def nat_trans_P_infty : ((alternating_face_map_complex C) âŸ¶
  (alternating_face_map_complex C)) :=
{ app := Î» _, P_infty,
  naturality' := Î» X Y f, begin
    ext n,
    simp only [homological_complex.comp_f, chain_complex.of_hom_f,
      alternating_face_map_complex_map, alternating_face_map_complex.map,
      P_infty_termwise_naturality],
  end }

@[simps]
def nat_trans_termwise_P_infty (n : â„•) :
  alternating_face_map_complex C â‹™ homological_complex.eval _ _ n âŸ¶
  alternating_face_map_complex C â‹™ homological_complex.eval _ _ n :=
nat_trans_P_infty C â—« ðŸ™ _

lemma map_hÏƒ' {D : Type*} [category.{v} D] [preadditive D]
  (G : C â¥¤ D) [G.additive] (X : simplicial_object C) (q : â„•) :
  (hÏƒ' q : prehomotopy ((alternating_face_map_complex D).obj (((whiskering C D).obj G).obj X)) _) =
  homotopy.map_prehomotopy G (hÏƒ' q : prehomotopy ((alternating_face_map_complex C).obj X) _) :=
begin
  ext ij,
  simp only [homotopy.map_prehomotopy],
  unfold hÏƒ' hÏƒ,
  split_ifs,
  { simp only [functor.map_zero, zero_comp], },
  { simp only [eq_to_hom_map, functor.map_comp, functor.map_zsmul],
    refl, },
end

lemma map_HÏƒ {D : Type*} [category.{v} D] [preadditive D]
  (G : C â¥¤ D) [G.additive] (X : simplicial_object C) (q n : â„•)
  : ((HÏƒ q : alternating_face_map_complex.obj (((whiskering C D).obj G).obj X) âŸ¶ _).f n) =
    G.map ((HÏƒ q : alternating_face_map_complex.obj X âŸ¶ _).f n) :=
begin
  unfold HÏƒ,
  have eq := (homological_complex.congr_hom
    (homotopy.map_null_homotopic_map G (hÏƒ' q : prehomotopy ((alternating_face_map_complex C).obj X) _)) n).symm,
  rw â† map_hÏƒ' at eq,
  dsimp at eq,
  rw â† eq,
  let h := (congr_obj (map_alternating_face_map_complex G) X).symm,
  congr',
end

lemma map_P {D : Type*} [category.{v} D] [preadditive D]
  (G : C â¥¤ D) [G.additive] (X : simplicial_object C) (q n : â„•)
  : ((P q : alternating_face_map_complex.obj (((whiskering C D).obj G).obj X) âŸ¶ _).f n) =
    G.map ((P q : alternating_face_map_complex.obj X âŸ¶ _).f n) :=
begin
  induction q with q hq,
  { unfold P,
    simp only [homological_complex.id_f],
    erw [G.map_id],
    refl, },
  { unfold P,
    simp only [comp_add, homological_complex.comp_f, homological_complex.add_f_apply, comp_id,
      functor.map_add, functor.map_comp, hq],
    congr' 2,
    rw map_HÏƒ, }
end

variables {C}

@[simp]
lemma map_P_infty_termwise {D : Type*} [category.{v} D] [preadditive D]
  (G : C â¥¤ D) [G.additive] (X : simplicial_object C) (n : â„•)
  : ((P_infty : alternating_face_map_complex.obj (((whiskering C D).obj G).obj X) âŸ¶ _).f n) =
    G.map ((P_infty : alternating_face_map_complex.obj X âŸ¶ _).f n) :=
by { simp only [P_infty_termwise, map_P], }

lemma karoubi_P_infty_f {X : karoubi (simplicial_object C)} (n : â„•) :
((P_infty : alternating_face_map_complex.obj ((karoubi_simplicial_object_functor C).obj X) âŸ¶ _).f n).f =
X.p.app (op [n]) â‰« ((P_infty : alternating_face_map_complex.obj X.X âŸ¶ _).f n) :=
begin
  let Pâ‚ : alternating_face_map_complex.obj ((karoubi_simplicial_object_functor C).obj X) âŸ¶ _ := P_infty,
  let Pâ‚‚ : alternating_face_map_complex.obj X.X âŸ¶ _ := P_infty,
  let Pâ‚ƒ : alternating_face_map_complex.obj (((whiskering _ _).obj (to_karoubi C)).obj X.X) âŸ¶ _ := P_infty,
  let Pâ‚„ : alternating_face_map_complex.obj ((karoubi_simplicial_object_functor C).obj X.X) âŸ¶ _ := P_infty,
  let Yâ‚„ := (karoubi_simplicial_object_functor C).obj X.X,
  have hâ‚ƒâ‚„ : Pâ‚ƒ.f n = Pâ‚„.f n := begin
    let Yâ‚ƒ :=  ((whiskering _ _).obj (to_karoubi C)).obj X.X,
    have hâ‚ƒ : (Pâ‚ƒ.f n) = ((nat_trans_termwise_P_infty (karoubi C) n).app Yâ‚ƒ) :=
      by simp only [nat_trans_termwise_P_infty_app, Pâ‚ƒ],
    have hâ‚„ : (Pâ‚„.f n) = ((nat_trans_termwise_P_infty (karoubi C) n).app Yâ‚„) :=
      by simp only [nat_trans_termwise_P_infty_app],
    rw [hâ‚ƒ, hâ‚„],
    congr,
    exact (congr_obj (to_karoubi_comp_karoubi_simplifical_object_functor C) X.X).symm,
  end,
  have hâ‚‚â‚ƒ : (Pâ‚ƒ.f n).f = Pâ‚‚.f n := karoubi.hom_ext.mp (map_P_infty_termwise (to_karoubi C) X.X n),
  have hâ‚â‚„ := (karoubi.nat_trans_eq
    ((ðŸ™ (karoubi_simplicial_object_functor C)) â—« nat_trans_termwise_P_infty (karoubi C) n) X),
  dsimp at hâ‚â‚„,
  simp only [nat_trans_termwise_P_infty_app] at hâ‚â‚„,
  erw comp_id at hâ‚â‚„,
  conv at hâ‚â‚„ { to_rhs, congr, skip, erw comp_id, },
  erw [hâ‚â‚„, â† hâ‚‚â‚ƒ, hâ‚ƒâ‚„],
  simp only [karoubi_simplicial_object.map_app_f, karoubi.comp],
  have eq := karoubi.hom_ext.mp (P_infty_termwise_naturality n 
    (((karoubi_simplicial_object_functor C).map âŸ¨X.p, by erw [karoubi.coe_p, comp_id, id_comp]âŸ©) : Yâ‚„ âŸ¶ Yâ‚„)),
  simp only [karoubi.comp, karoubi_simplicial_object_functor_map,
    karoubi_simplicial_object.map_app_f] at eq,
  erw [â† eq, â† assoc],
  congr,
  simpa only [nat_trans.comp_app] using congr_app X.idempotence (op [n]),
end

/-- Q q is the complement projector associated to P q -/
def Q {X : simplicial_object C} (q : â„•) : ((alternating_face_map_complex C).obj X âŸ¶ 
(alternating_face_map_complex C).obj X) := ðŸ™ _ - P q

/-- This is the decreasing involution of `fin (n+1)` which appears in `decomposition_Q`. -/
def reverse_fin {n : â„•} (i : fin(n+1)) : fin(n+1):= âŸ¨n-i, nat.sub_lt_succ n â†‘iâŸ©

lemma reverse_fin_eq {n a : â„•} (i : fin(n+1)) (hnaq : n=a+i) : reverse_fin i = 
  âŸ¨a, nat.lt_succ_iff.mpr (nat.le.intro (eq.symm hnaq))âŸ© :=
begin
  ext,
  simp only [reverse_fin, fin.coe_mk],
  exact tsub_eq_of_eq_add hnaq,
end

/-- We decompose the identity using `P_q` and degeneracies. In the case of a simplicial
abelian group, this means we can decompose a $(n+1)$-simplex $x$ as
$x = x' + \sum (i=0}^{q-1} Ïƒ_{n-i}(y_i)$ where $x'$ is in the image of `P_q$ and
the $y_i$ are in degree $n$. -/
lemma decomposition_Q {X : simplicial_object C} (n q : â„•) (hqn : qâ‰¤n+1) :
  ((Q q).f (n+1) : X _[n+1] âŸ¶ X _[n+1]) =
  âˆ‘ (i : fin(n+1)) in finset.filter (Î» i : fin(n+1), (i:â„•)<q) finset.univ,
    (P i).f (n+1) â‰« X.Î´ (reverse_fin i).succ â‰« X.Ïƒ (reverse_fin i) :=
begin
  revert hqn,
  induction q with q hq,
  { intro hqn,
    simp only [Q, P, nat.not_lt_zero, finset.sum_empty, finset.filter_false,
      homological_complex.zero_f_apply, sub_self], },
  { intro hqn,
    rw [leave_out_last_term (nat.succ_le_iff.mp hqn), â† hq (nat.le_of_succ_le hqn)],
    cases nat.le.dest (nat.succ_le_succ_iff.mp hqn) with a ha,
    let i : fin(n+1) := âŸ¨q,nat.lt_succ_iff.mpr (nat.le.intro ha)âŸ©,
    simp only [fin.succ_mk, fin.coe_mk, norm_num.sub_nat_pos n q a ha,
      reverse_fin_eq i (show n=a+i, by { simp only [fin.coe_mk, add_comm, ha], })],
    have eq : ((_ : X _[n+1] âŸ¶ _) = _ ) := eq_neg_of_eq_neg 
      (HÏƒÏ†_eq_neg_ÏƒÎ´ (show n=a+q, by linarith) (higher_faces_vanish_P q n)),
    rw eq,
    unfold Q P,
    simp only [homological_complex.sub_f_apply, comp_add, homological_complex.comp_f,
      homological_complex.add_f_apply, homological_complex.id_f, comp_id],
    abel,
  },
end

/-- The structure `morph_components` is an ad hoc structure that is used the 
proof of `normalized_Moore_complex_reflects_iso`. The fields are the data
that are needed in order to construct a morphism `X _[n+1] âŸ¶ Z` (see `F`)
using the decomposition of the identity given by `decomposition_Q n (n+1)`.

In the proof of `normalized_Moore_complex_reflects_iso`, in order to check
that two maps coincide, we only need to verify that the `morph_components`
they come from are equal.
-/
@[ext, nolint has_inhabited_instance]
structure morph_components (X : simplicial_object C) (n : â„•) (Z : C) :=
  (a : X _[n+1] âŸ¶ Z) (b : fin(n+1) â†’ (X _[n] âŸ¶ Z))

/-- The morphism `X _[n+1] âŸ¶ Z ` associated to a `morph_components X n Z`-/
def F {Z : C} {n : â„•} {X : simplicial_object C} (f : morph_components X n Z) :
  X _[n+1] âŸ¶ Z :=
  P_infty.f (n+1) â‰« f.a + âˆ‘ (i : fin (n+1)),
  (((P i).f (n+1)) â‰« (X.Î´ (reverse_fin i).succ) â‰« (f.b (reverse_fin i))) 

/-- the canonical `morph_components` whose associated morphism is the identity
(see `F_id`) thanks to `decomposition_Q n (n+1)` -/
@[simps]
def morph_components_id (X : simplicial_object C) (n : â„•) :
  morph_components X n (X _[n+1]) :=
{ a := P_infty.f (n+1),
  b := Î» i, X.Ïƒ i, }

lemma F_id (X : simplicial_object C) (n : â„•) :
  F (morph_components_id X n) = ðŸ™ _ :=
begin
  simp only [morph_components_id, F, P_infty_termwise,
    â† homological_complex.comp_f, P_is_a_projector (n+1),
    (show ðŸ™ (X.obj (op [n+1])) = (P (n+1)).f (n+1)+(Q (n+1)).f (n+1), by
  { simp only [Q, homological_complex.sub_f_apply, add_sub_cancel'_right,
    homological_complex.id_f], refl, })],
  congr,
  rw decomposition_Q n (n+1) rfl.ge,
  congr,
  ext,
  simp only [true_and, true_iff, finset.mem_univ, finset.mem_filter, fin.is_lt],
end

/-- A `morph_components` can be postcomposed with a map `Z âŸ¶ Z'`. -/
@[simps]
def morph_components_comp {X : simplicial_object C} {n : â„•} {Z Z' : C}
  (f : morph_components X n Z) (g : Z âŸ¶ Z') : morph_components X n Z' :=
{ a := f.a â‰« g,
  b := Î» i, f.b i â‰« g }

lemma F_comp {X : simplicial_object C} {n : â„•} {Z Z' : C} (f : morph_components X n Z)
  (g : Z âŸ¶ Z') : F (morph_components_comp f g) = F f â‰« g :=
begin
  unfold F morph_components_comp,
  simp only [add_comp, sum_comp, assoc],
end

/-- A `morph_components` can be precomposed with a map `X' âŸ¶ X`. -/
@[simps]
def comp_morph_components {X' X : simplicial_object C} {n : â„•} {Z : C}
  (g : X' âŸ¶ X) (f : morph_components X n Z) : morph_components X' n Z :=
{ a := g.app (op [n+1]) â‰« f.a,
  b := Î» i, g.app (op [n]) â‰« f.b i }

lemma comp_F {X' X : simplicial_object C} {n : â„•} {Z : C}
  (g : X' âŸ¶ X) (f : morph_components X n Z) :
  F (comp_morph_components g f) = g.app (op [n+1]) â‰« F f :=
begin
  unfold F comp_morph_components,
  simp only [P_infty_termwise, comp_add],
  congr' 1,
  { simp only [â† assoc, P_termwise_naturality], },
  { simp only [comp_sum],
    congr,
    ext,
    slice_rhs 1 2 {rw P_termwise_naturality, },
    slice_lhs 2 3 {erw g.naturality, },
    simp only [assoc],
    refl, }
end

namespace N'_functor

@[simps]
def obj (X : simplicial_object C) : karoubi (chain_complex C â„•) :=
  âŸ¨(alternating_face_map_complex C).obj X, P_infty, P_infty_is_a_projectorâŸ©

@[simps]
def map {X Y : simplicial_object C} (f : X âŸ¶ Y) : obj X âŸ¶ obj Y :=
  âŸ¨P_infty â‰« (alternating_face_map_complex C).map f,
begin
  ext n,
  dsimp [P_infty],
  conv { to_lhs, congr, rw [â† P_is_a_projector, homological_complex.comp_f], },
  slice_lhs 2 3 { rw â† P_termwise_naturality, },
  slice_rhs 1 2 { rw [â† homological_complex.comp_f,
    P_is_a_projector], },
  rw assoc,
endâŸ©

end N'_functor

@[simps]
def N' : simplicial_object C â¥¤ karoubi (chain_complex C â„•) :=
{ obj := N'_functor.obj,
  map := Î» X Y f, N'_functor.map f,
  map_id' := Î» X, begin
    ext n,
    simp only [homological_complex.comp_f, chain_complex.of_hom_f,
      nat_trans.id_app, alternating_face_map_complex_map,
      alternating_face_map_complex.map, karoubi.id_eq, N'_functor.map_f, N'_functor.obj_p],
    erw comp_id,
  end,
  map_comp' := Î» X Y Z f g, begin
    ext n,
    simp only [homological_complex.comp_f, karoubi.comp,
      alternating_face_map_complex.map, alternating_face_map_complex_map,
      chain_complex.of_hom_f, nat_trans.comp_app, P_infty, N'_functor.map_f],
      slice_rhs 2 3 { erw P_termwise_naturality, },
      slice_rhs 1 2 { rw [â† homological_complex.comp_f,
        P_is_a_projector], },
      rw assoc,
  end }

variable (C)

theorem N'_reflects_iso : reflects_isomorphisms
  (N' : simplicial_object C â¥¤ karoubi (chain_complex C â„•)) :=
begin
  refine âŸ¨_âŸ©,
  intros X Y f,
  introI,
  /- restating the result in a way that allows induction on the degree n -/
  haveI : âˆ€ (Î” : simplex_categoryáµ’áµ–), is_iso (f.app Î”), swap,
  { exact nat_iso.is_iso_of_is_iso_app f, },
  intro Î”,
  let m := simplex_category.len (unop Î”),
  rw [show Î” = op [m], by { simp only [op_unop, simplex_category.mk_len], }],
  generalize : m = n, clear m Î”,
  /- rewriting some assumptions in a more practical form -/
  have h  := homological_complex.congr_hom (karoubi.hom_ext.mp
    (is_iso.hom_inv_id (N'.map f))),
  have h' := homological_complex.congr_hom (karoubi.hom_ext.mp
    (is_iso.inv_hom_id (N'.map f))),
  simp only [N'_map, homological_complex.comp_f, chain_complex.of_hom_f, assoc,
    karoubi.id_eq, karoubi.comp, alternating_face_map_complex_map,
    alternating_face_map_complex.map] at h h',
  dsimp at h h',
  /- we have to construct an inverse to f in degree n, by induction on n -/
  induction n with n hn,
  /- degree 0 -/
  { use (inv (N'.map f)).f.f 0,
    split,
    have eq := h 0, swap,
    have eq := h' 0,
    all_goals
    { simp only [P_infty_termwise, P_deg0_eq] at eq,
      erw id_comp at eq,
      exact eq, }, },
  /- isomorphism in degree n+1 of an isomorphism in degree n -/
  { resetI,
    use F
      { a := P_infty.f (n+1) â‰«  (inv (N'.map f)).f.f (n+1),
        b := Î» i, inv (f.app (op [n])) â‰« X.Ïƒ i, },
    split,
    { rw [â† F_id, â† comp_F],
      simp only [comp_morph_components, morph_components_id],
      congr' 2,
      { erw [â† assoc, P_infty_termwise_naturality],
        exact h (n+1), },
      { ext,
        rw â† assoc,
        simp only [id_comp, is_iso.hom_inv_id], }, },
    { rw [â† F_id, â† F_comp],
      simp only [morph_components_comp, morph_components_id],
      congr' 2,
      { have eq := homological_complex.congr_hom (karoubi.p_comp (inv (N'.map f))) (n+1),
        have eq' := homological_complex.congr_hom (karoubi.comp_p (inv (N'.map f))) (n+1),
        simp only [homological_complex.comp_f] at eq eq',
        erw [eq, â† eq', assoc],
        exact h' (n+1), },
      { ext,
        erw [assoc, f.naturality, â† assoc, is_iso.inv_hom_id, id_comp],
        refl, }, }, }
end

variables {C}
@[simps]
def N : karoubi (simplicial_object C) â¥¤ karoubi (chain_complex C â„•) :=
  karoubi.functor_extension' N'

lemma karoubi_alternating_face_map_complex_d (X : karoubi (simplicial_object C)) (n : â„•) :
  ((((alternating_face_map_complex (karoubi C)).obj
    ((karoubi_simplicial_object_functor C).obj X)).d (n+1) n).f : X.X _[n+1] âŸ¶ X.X _[n])
  = X.p.app (op [n+1]) â‰« (((alternating_face_map_complex C).obj X.X).d (n+1) n) â‰« X.p.app (op [n]) :=
begin
  have hâ‚â‚„ := (karoubi.hom_ext.mp (((karoubi_simplicial_object_functor C â‹™ (alternating_face_map_complex (karoubi C))).map (ðŸ™ X)).comm' (n+1) n (cs_down_succ n))).symm,
  conv at hâ‚â‚„ { to_lhs, erw functor.map_id', },
  simp only [homological_complex.id_f, comp_id] at hâ‚â‚„,
  rw [karoubi.decomp_id, functor.map_comp, homological_complex.comp_f, assoc,
    ((karoubi_simplicial_object_functor C â‹™ (alternating_face_map_complex (karoubi C))).map
    (karoubi.decomp_id_p X)).comm' (n+1) n (cs_down_succ n)] at hâ‚â‚„,
  simp only [karoubi.comp] at hâ‚â‚„,
  have hâ‚„â‚ƒ := homological_complex.congr_d (congr_arg alternating_face_map_complex.obj
    (congr_obj (to_karoubi_comp_karoubi_simplifical_object_functor C) X.X)) (n+1) n (cs_down_succ n),
  simp only [eq_to_hom_refl, comp_id, id_comp] at hâ‚„â‚ƒ,
  erw [hâ‚„â‚ƒ] at hâ‚â‚„,
  have hâ‚‚â‚ƒ := karoubi.hom_ext.mp (homological_complex.congr_d
    (congr_obj (map_alternating_face_map_complex (to_karoubi C)) X.X) (n+1) n (cs_down_succ n)),
  simp only [functor.comp_obj, eq_to_hom_refl, comp_id, id_comp, functor.map_homological_complex_obj_d, karoubi.comp,
    to_karoubi_map_f, karoubi.id_eq] at hâ‚‚â‚ƒ,
  dsimp at hâ‚‚â‚ƒ,
  simp only [id_comp, comp_id] at hâ‚‚â‚ƒ,
  simpa [â† hâ‚‚â‚ƒ] using hâ‚â‚„,
end

variables (C)

theorem N_reflects_iso : reflects_isomorphisms
  (N : karoubi (simplicial_object C) â¥¤ karoubi (chain_complex C â„•)) :=
begin
  refine âŸ¨_âŸ©,
  intros X Y f,
  introI,
  let F1 := karoubi_simplicial_object_functor C,
  let F2 : simplicial_object (karoubi C) â¥¤ _ := N',
  let F3 := (karoubi_chain_complex_equivalence (karoubi C) â„•).functor,
  let F4 := functor.map_homological_complex (karoubi_karoubi_equivalence C).inverse (complex_shape.down â„•),
  haveI : reflects_isomorphisms F2 := N'_reflects_iso _,
  haveI : is_iso ((F1 â‹™ F2 â‹™ F3 â‹™ F4).map f), swap,
  { exact is_iso_of_reflects_iso f (F1 â‹™ F2 â‹™ F3 â‹™ F4), },
  let F5 := (karoubi_chain_complex_equivalence C â„•).functor,
  have eq : F1 â‹™ F2 â‹™ F3 â‹™ F4 = N â‹™ F5, swap,
  { rw eq,
    simp only [functor.comp_map],
    exact functor.map_is_iso F5 (N.map f), },
  { apply functor_ext,
    { intros P Q f,
      ext n,
      dsimp [F3, F5],
      simp [karoubi_P_infty_f],
      slice_lhs 3 4 { rw [â† nat_trans.comp_app, congr_app (karoubi.comp_p f) (op [n])] },
      rw P_infty_termwise_naturality, },
    { intro P,
      ext i j hij,
      { dsimp [F3, F5],
        simp only [karoubi.comp, karoubi.eq_to_hom_f, eq_to_hom_refl,
          karoubi_karoubi.inverse_map_f, karoubi_karoubi.inverse_obj_p,
          karoubi_chain_complex_equivalence_functor_obj_d_f,
          karoubi_chain_complex_equivalence_functor_obj_X_p, comp_id, assoc],
        have h := karoubi.hom_ext.mp (homological_complex.congr_hom (N'_functor.obj
          (karoubi_simplicial_object.obj P)).idempotence j),
        simp only [homological_complex.comp_f, karoubi.comp] at h,
        conv { to_lhs, congr, skip, rw h, },
        dsimp only [N'_functor.obj_X, N'_functor.obj_p],
        simp only [N_obj_p_f],
        have h : j+1=i := hij,
        subst h,
        erw karoubi_alternating_face_map_complex_d P j,
        repeat { erw karoubi_P_infty_f, },
        have eq := congr_app P.idempotence (op [j]),
        simp only [nat_trans.comp_app] at eq,
        slice_lhs 3 4 { rw eq, },
        slice_lhs 3 4 { rw P_infty_termwise_naturality, },
        slice_rhs 2 3 { erw P_infty.comm (j+1) j, },
        slice_rhs 3 4 { rw P_infty_termwise_is_a_projector, }, },
      { ext n,
        { dsimp,
          simp only [comp_id, id_comp],
          rw [karoubi_P_infty_f, P_infty_termwise_naturality], },
        { refl, }, }, }, },
end

end dold_kan

end algebraic_topology
